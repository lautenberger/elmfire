MODULE ELMFIRE_IO

USE ELMFIRE_VARS
USE ELMFIRE_SUBS

IMPLICIT NONE

CONTAINS

! *****************************************************************************
SUBROUTINE SETUP_PARALLEL_IO
! *****************************************************************************

INTEGER :: I,J,N

IF (LEN_TRIM(POPULATION_DENSITY_FILENAME) .GT. 0 .AND. USE_POPULATION_DENSITY) THEN
   USE_POPULATION_DENSITY = .TRUE. 
ELSE
   USE_POPULATION_DENSITY = .FALSE.
ENDIF

IF (LEN_TRIM(REAL_ESTATE_VALUE_FILENAME) .GT. 0 .AND. USE_REAL_ESTATE_VALUE) THEN
   USE_REAL_ESTATE_VALUE = .TRUE.
ELSE
   USE_REAL_ESTATE_VALUE = .FALSE.
ENDIF

IF (LEN_TRIM(LAND_VALUE_FILENAME) .GT. 0 .AND. USE_LAND_VALUE) THEN
   USE_LAND_VALUE = .TRUE. 
ELSE
   USE_LAND_VALUE = .FALSE.
ENDIF

N = NPROC_IRANK0
PARALLEL_IO_RANK(:) = -1
IF (N .GT. 1) THEN

! 1 : WS header
! 2 : ASP header
   PARALLEL_IO_RANK(1) = 0
   PARALLEL_IO_RANK(2) = 1

! 3: WS
! 4: WD
! 5: M1
! 6: M10
! 7: M100
   I = 0
   DO J = 3, 7
      PARALLEL_IO_RANK(J) = I
      I = I + 1
      IF (I .GT. N - 1) I = 0
   ENDDO

! 8: ERC
   IF (USE_ERC) THEN
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(8) = I
   ENDIF

!  9: LH
! 10: LW 
! 11: MFOL
   DO J = 9, 11
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(J) = I
   ENDDO

! 12: IGN_MASK
   I = 0
   IF (USE_IGNITION_MASK) THEN
      PARALLEL_IO_RANK(12) = I
      I = I + 1
   ENDIF

! 13: ASP
! 14: CBH
! 15: CBD
! 16: CC
! 17: CH
! 18: DEM
! 19: FBFM
! 20: SLP
! 21: ADJ
! 22: PHI0
   DO J = 13, 22
      PARALLEL_IO_RANK(J) = I
      I = I + 1
      IF (I .GT. N - 1) I = 0
   ENDDO

! 23: POPULATION_DENSITY
   IF (USE_POPULATION_DENSITY) THEN
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(23) = I
   ENDIF

! 24: REAL_ESTATE_VALUE
   IF (USE_REAL_ESTATE_VALUE) THEN
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(24) = I
   ENDIF

! 25: LAND_VALUE
   IF (USE_LAND_VALUE) THEN
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(25) = I
   ENDIF

! 26: CALIBRATION_TARGET - deprecated

! 27: SDI
   IF (USE_SDI) THEN
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(27) = I
   ENDIF

! 28-32: Building spread (previously Hamada model) parameters
   IF (USE_BLDG_SPREAD_MODEL) THEN
      DO J = 28, 32
         I = I + 1
         IF (I .GT. N - 1) I = 0
         PARALLEL_IO_RANK(J) = I
      ENDDO
   ELSE
      IF (ENABLE_SPOTTING .AND. (.NOT. USE_SUPERSEDED_SPOTTING) ) THEN
         I = I + 1
         IF (I .GT. N - 1) I = 0
         PARALLEL_IO_RANK(31) = I
      ENDIF
   ENDIF

! 33: Pyromes
   IF (USE_PYROMES) THEN
      I = I + 1
      IF (I .GT. N - 1) I = 0
      PARALLEL_IO_RANK(33) = I
   ENDIF

! 34: TIMES_BURNED
! 35: SURFACE_FIRE_AREA
! 36: CROWN_FIRE_AREA
! 37: SURFACE_FIRE_VOLUME
! 38: AFFECTED_POPULATION
! 39: AFFECTED_REAL_ESTATE_VALUE
! 40: AFFECTED_LAND_VALUE
   I = 0
   DO J = 34, 40
      PARALLEL_IO_RANK(J) = I
      I = I + 1
      IF (I .GT. N - 1) I = 0
   ENDDO
ELSE
   PARALLEL_IO_RANK(:) = 0
ENDIF

! *****************************************************************************
END SUBROUTINE SETUP_PARALLEL_IO
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_WEATHER_FUEL_TOPOGRAPHY
! *****************************************************************************

INTEGER :: ICOL, IROW
REAL :: CONSTANT_LH, CONSTANT_LW, CONSTANT_FMC, RX
INTEGER :: IERR
LOGICAL, PARAMETER :: NOISE=.TRUE.

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(3)) THEN
   CALL READ_BSQ_RASTER (WS,   WEATHER_DIRECTORY, WS_FILENAME)
   IF (WS_AT_10M) WHERE(WS%R4(:,:,:) .NE. WS%NODATA_VALUE) WS%R4(:,:,:) = 0.87 * WS%R4(:,:,:)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(4)) CALL READ_BSQ_RASTER (WD,   WEATHER_DIRECTORY, WD_FILENAME)

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(5)) THEN
   CALL READ_BSQ_RASTER (M1,   WEATHER_DIRECTORY, M1_FILENAME)
   IF (DEAD_MC_IN_PERCENT) WHERE (M1%R4  (:,:,:) .NE. M1%NODATA_VALUE  ) M1%R4  (:,:,:) = 0.01 * M1%R4  (:,:,:)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(6)) THEN
   CALL READ_BSQ_RASTER (M10,  WEATHER_DIRECTORY, M10_FILENAME)
   IF (DEAD_MC_IN_PERCENT) WHERE (M10%R4 (:,:,:) .NE. M10%NODATA_VALUE ) M10%R4 (:,:,:) = 0.01 * M10%R4 (:,:,:)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(7)) THEN
   CALL READ_BSQ_RASTER (M100, WEATHER_DIRECTORY, M100_FILENAME)
   IF (DEAD_MC_IN_PERCENT) WHERE (M100%R4(:,:,:) .NE. M100%NODATA_VALUE) M100%R4(:,:,:) = 0.01 * M100%R4(:,:,:)
ENDIF

IF (USE_ERC .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(8) ) THEN
   CALL READ_BSQ_RASTER(ERC, WEATHER_DIRECTORY, ERC_FILENAME)
   CALL ERC_IGNITION_FACTOR (ERC, IGNFAC, 1, ERC%NBANDS)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(9)) THEN
   CONSTANT_LH  = -9999.
   IF (USE_CONSTANT_LH) THEN
      CONSTANT_LH = LH_MOISTURE_CONTENT
      IF (LIVE_MC_IN_PERCENT) CONSTANT_LH = CONSTANT_LH * 0.01
   ENDIF
   IF (CONSTANT_LH .GT. 0. ) THEN
      MLH%R4 (:,:,:) = CONSTANT_LH
   ELSE
      CALL READ_BSQ_RASTER(MLH, WEATHER_DIRECTORY, MLH_FILENAME)
      IF (LIVE_MC_IN_PERCENT) MLH%R4(:,:,:) = 0.01 * MLH%R4(:,:,:)
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(10)) THEN
   CONSTANT_LW  = -9999.
   IF (USE_CONSTANT_LW) THEN
      CONSTANT_LW = LW_MOISTURE_CONTENT
      IF (LIVE_MC_IN_PERCENT) CONSTANT_LW = CONSTANT_LW * 0.01
   ENDIF
   IF (CONSTANT_LW .GT. 0. ) THEN
      MLW%R4 (:,:,:) = CONSTANT_LW
   ELSE
      CALL READ_BSQ_RASTER(MLW, WEATHER_DIRECTORY, MLW_FILENAME)
      IF (LIVE_MC_IN_PERCENT) MLW%R4(:,:,:) = 0.01 * MLW%R4(:,:,:)
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(11)) THEN
   CONSTANT_FMC = -9999.
   IF (USE_CONSTANT_FMC) THEN
      CONSTANT_FMC = FOLIAR_MOISTURE_CONTENT
   ENDIF
   IF (CONSTANT_FMC .GT. 0. ) THEN
      MFOL%R4 (:,:,:) = CONSTANT_FMC
   ELSE
      CALL READ_BSQ_RASTER(MFOL, WEATHER_DIRECTORY, FMC_FILENAME)
   ENDIF
ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
IF (NPROC .GT. 1) THEN
   CALL MPI_BCAST_RASTER_HEADER(WS   , PARALLEL_IO_RANK(3), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(WD   , PARALLEL_IO_RANK(4), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(M1   , PARALLEL_IO_RANK(5), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(M10  , PARALLEL_IO_RANK(6), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(M100 , PARALLEL_IO_RANK(7), .FALSE.)
   IF (USE_ERC) THEN
      CALL MPI_BCAST_RASTER_HEADER(ERC    , PARALLEL_IO_RANK(8), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(IGNFAC , PARALLEL_IO_RANK(8), .FALSE.)
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Reading fuel and topography rasters'

IF (USE_IGNITION_MASK .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(12)) THEN
   CALL READ_BSQ_RASTER (IGN_MASK, FUELS_AND_TOPOGRAPHY_DIRECTORY, IGNITION_MASK_FILENAME)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(13)) THEN
   CALL READ_BSQ_RASTER (ASP , FUELS_AND_TOPOGRAPHY_DIRECTORY, ASP_FILENAME)
   IF (TRIM(ASP%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      ASP%R4(:,:,1) = REAL(ASP%I2(:,:,1))
      DEALLOCATE(ASP%I2)
      ASP%PIXELTYPE='FLOAT     '
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(14)) THEN
   CALL READ_BSQ_RASTER (CBH , FUELS_AND_TOPOGRAPHY_DIRECTORY, CBH_FILENAME)
   IF (TRIM(CBH%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CBH%R4(:,:,1) = REAL(CBH%I2(:,:,1))
      DEALLOCATE(CBH%I2)
      CBH%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CBH_TIMES_10 ) WHERE(CBH%R4(:,:,1) .NE. CBH%NODATA_VALUE) CBH%R4(:,:,1) = 0.10*CBH%R4(:,:,1) ! m
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(15)) THEN
   CALL READ_BSQ_RASTER (CBD , FUELS_AND_TOPOGRAPHY_DIRECTORY, CBD_FILENAME)
   IF (TRIM(CBD%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CBD%R4(:,:,1) = REAL(CBD%I2(:,:,1))
      DEALLOCATE(CBD%I2)
      CBD%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CBD_TIMES_100) WHERE(CBD%R4(:,:,1) .NE. CBD%NODATA_VALUE) CBD%R4(:,:,1) = 0.01*CBD%R4(:,:,1) ! kg/m3
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(16)) THEN
   CALL READ_BSQ_RASTER (CC  , FUELS_AND_TOPOGRAPHY_DIRECTORY, CC_FILENAME)
   IF (TRIM(CC%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CC%R4(:,:,1) = REAL(CC%I2(:,:,1))
      DEALLOCATE(CC%I2)
      CC%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CC_IN_PERCENT) WHERE(CC%R4 (:,:,1) .NE. CC%NODATA_VALUE ) CC%R4 (:,:,1) = 0.01*CC%R4 (:,:,1) ! -
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(17)) THEN
   CALL READ_BSQ_RASTER (CH  , FUELS_AND_TOPOGRAPHY_DIRECTORY, CH_FILENAME )
   IF (TRIM(CH%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CH%R4(:,:,1) = REAL(CH%I2(:,:,1))
      DEALLOCATE(CH%I2)
      CH%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CH_TIMES_10  ) WHERE(CH%R4 (:,:,1) .NE. CH%NODATA_VALUE ) CH%R4 (:,:,1) = 0.10*CH%R4 (:,:,1) ! m
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(18) .AND. MODE .NE. 2) THEN
   CALL READ_BSQ_RASTER (DEM , FUELS_AND_TOPOGRAPHY_DIRECTORY, DEM_FILENAME)
   IF (TRIM(DEM%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      DEM%R4(:,:,1)= REAL(DEM%I2(:,:,1))
      DEALLOCATE(DEM%I2)
      DEM%PIXELTYPE='FLOAT     '
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(19)) CALL READ_BSQ_RASTER (FBFM, FUELS_AND_TOPOGRAPHY_DIRECTORY, FBFM_FILENAME)

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(20)) THEN
   CALL READ_BSQ_RASTER (SLP , FUELS_AND_TOPOGRAPHY_DIRECTORY, SLP_FILENAME)
   IF (TRIM(SLP%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      SLP%R4(:,:,1) = REAL(SLP%I2(:,:,1))
      DEALLOCATE(SLP%I2)
      SLP%PIXELTYPE='FLOAT     '
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(21) ) THEN
    CALL READ_BSQ_RASTER (ADJ , FUELS_AND_TOPOGRAPHY_DIRECTORY, ADJ_FILENAME)
    WHERE(ADJ%R4 .LT. 0.) ADJ%R4=1.
ENDIF
IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(22) .AND. MODE .NE. 2) THEN
   CALL READ_BSQ_RASTER (PHI0 ,FUELS_AND_TOPOGRAPHY_DIRECTORY, PHI_FILENAME)
   WHERE(PHI0%R4(:,:,1) .LT.   -1.1) PHI0%R4(:,:,1) = 1.0
   IF (NOISE) THEN
      DO IROW = 1, PHI0%NROWS
      DO ICOL = 1, PHI0%NCOLS
         CALL RANDOM_NUMBER(RX)
         PHI0%R4(ICOL,IROW,1) = PHI0%R4(ICOL,IROW,1) + 1E-3 * (0.5 - RX)
      ENDDO
      ENDDO
   ENDIF
ENDIF

IF (USE_POPULATION_DENSITY .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(23)) CALL READ_BSQ_RASTER (POPULATION_DENSITY, FUELS_AND_TOPOGRAPHY_DIRECTORY, POPULATION_DENSITY_FILENAME)
IF (USE_REAL_ESTATE_VALUE  .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(24)) CALL READ_BSQ_RASTER (REAL_ESTATE_VALUE , FUELS_AND_TOPOGRAPHY_DIRECTORY, REAL_ESTATE_VALUE_FILENAME )
IF (USE_LAND_VALUE         .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(25)) CALL READ_BSQ_RASTER (LAND_VALUE        , FUELS_AND_TOPOGRAPHY_DIRECTORY, LAND_VALUE_FILENAME        )
IF (USE_SDI                .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(27)) CALL READ_BSQ_RASTER (SDI               , FUELS_AND_TOPOGRAPHY_DIRECTORY, SDI_FILENAME               )

IF (USE_BLDG_SPREAD_MODEL) THEN
   IF (USE_CONSTANT_BLDG_SPREAD_MODEL_PARAMS) THEN
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(28)) BLDG_AREA%R4            (:,:,:) = BLDG_AREA_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(29)) BLDG_SEPARATION_DIST%R4 (:,:,:) = BLDG_SEPARATION_DIST_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(30)) BLDG_NONBURNABLE_FRAC%R4(:,:,:) = BLDG_NONBURNABLE_FRAC_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(31)) BLDG_FOOTPRINT_FRAC%R4  (:,:,:) = BLDG_FOOTPRINT_FRAC_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(32)) BLDG_FUEL_MODEL%I2      (:,:,:) = BLDG_FUEL_MODEL_CONSTANT
   ELSE
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(28)) CALL READ_BSQ_RASTER (BLDG_AREA            , FUELS_AND_TOPOGRAPHY_DIRECTORY, BLDG_AREA_FILENAME )
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(29)) CALL READ_BSQ_RASTER (BLDG_SEPARATION_DIST , FUELS_AND_TOPOGRAPHY_DIRECTORY, BLDG_SEPARATION_DIST_FILENAME )
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(30)) CALL READ_BSQ_RASTER (BLDG_NONBURNABLE_FRAC, FUELS_AND_TOPOGRAPHY_DIRECTORY, BLDG_NONBURNABLE_FRAC_FILENAME)
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(31)) CALL READ_BSQ_RASTER (BLDG_FOOTPRINT_FRAC  , FUELS_AND_TOPOGRAPHY_DIRECTORY, BLDG_FOOTPRINT_FRAC_FILENAME)
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(32)) CALL READ_BSQ_RASTER (BLDG_FUEL_MODEL      , FUELS_AND_TOPOGRAPHY_DIRECTORY, BLDG_FUEL_MODEL_FILENAME)
   ENDIF
ELSE
   IF (ENABLE_SPOTTING .AND. (.NOT. USE_SUPERSEDED_SPOTTING) ) THEN
      IF (USE_CONSTANT_BLDG_SPREAD_MODEL_PARAMS) THEN
         IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(31)) BLDG_FOOTPRINT_FRAC%R4  (:,:,:) = BLDG_FOOTPRINT_FRAC_CONSTANT
      ELSE
         IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(31)) CALL READ_BSQ_RASTER (BLDG_FOOTPRINT_FRAC  , FUELS_AND_TOPOGRAPHY_DIRECTORY, BLDG_FOOTPRINT_FRAC_FILENAME)
      ENDIF
   ENDIF
ENDIF

IF (USE_PYROMES .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(33)) CALL READ_BSQ_RASTER (PYROMES, FUELS_AND_TOPOGRAPHY_DIRECTORY, PYROMES_FILENAME)

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
IF (NPROC .GT. 1) THEN
   IF (USE_IGNITION_MASK) CALL MPI_BCAST_RASTER_HEADER(IGN_MASK  , PARALLEL_IO_RANK(12), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(ASP       , PARALLEL_IO_RANK(13), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CBH       , PARALLEL_IO_RANK(14), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CBD       , PARALLEL_IO_RANK(15), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CC        , PARALLEL_IO_RANK(16), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CH        , PARALLEL_IO_RANK(17), .FALSE.)
   IF (MODE .NE. 2) CALL MPI_BCAST_RASTER_HEADER(DEM  , PARALLEL_IO_RANK(18), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(FBFM      , PARALLEL_IO_RANK(19), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(SLP       , PARALLEL_IO_RANK(20), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(ADJ       , PARALLEL_IO_RANK(21), .FALSE.)
   IF (MODE .NE. 2) CALL MPI_BCAST_RASTER_HEADER(PHI0 , PARALLEL_IO_RANK(22), .FALSE.)
   IF (USE_POPULATION_DENSITY) CALL MPI_BCAST_RASTER_HEADER(POPULATION_DENSITY , PARALLEL_IO_RANK(23), .FALSE.)
   IF (USE_REAL_ESTATE_VALUE ) CALL MPI_BCAST_RASTER_HEADER(REAL_ESTATE_VALUE   ,PARALLEL_IO_RANK(24), .FALSE.)
   IF (USE_LAND_VALUE        ) CALL MPI_BCAST_RASTER_HEADER(LAND_VALUE         , PARALLEL_IO_RANK(25), .FALSE.)
   IF (USE_SDI               ) CALL MPI_BCAST_RASTER_HEADER(SDI                , PARALLEL_IO_RANK(27), .FALSE.)
   IF (USE_BLDG_SPREAD_MODEL) THEN
      CALL MPI_BCAST_RASTER_HEADER(BLDG_AREA            , PARALLEL_IO_RANK(28), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_SEPARATION_DIST , PARALLEL_IO_RANK(29), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_NONBURNABLE_FRAC, PARALLEL_IO_RANK(30), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_FOOTPRINT_FRAC  , PARALLEL_IO_RANK(31), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_FUEL_MODEL      , PARALLEL_IO_RANK(32), .FALSE.)
   ENDIF
   IF (USE_PYROMES) CALL MPI_BCAST_RASTER_HEADER(PYROMES, PARALLEL_IO_RANK(33), .FALSE.)
ENDIF

! *****************************************************************************
END SUBROUTINE READ_WEATHER_FUEL_TOPOGRAPHY
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_WEATHER_FUEL_TOPOGRAPHY_TILED
! *****************************************************************************

INTEGER :: ICOL, IROW
REAL :: CONSTANT_LH, CONSTANT_LW, CONSTANT_FMC, RX
INTEGER :: IERR
LOGICAL, PARAMETER :: NOISE=.TRUE.
CHARACTER(400) :: FN

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(3)) THEN
   FN = TRIM(WEATHER_DIRECTORY) // TRIM(WS_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (WS,FN)
   IF (WS_AT_10M) WHERE(WS%R4(:,:,:) .NE. WS%NODATA_VALUE) WS%R4(:,:,:) = 0.87 * WS%R4(:,:,:) 
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(4)) THEN
   FN = TRIM(WEATHER_DIRECTORY) // TRIM(WD_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (WD,FN)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(5)) THEN
   FN = TRIM(WEATHER_DIRECTORY) // TRIM(M1_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (M1,FN)
   IF (DEAD_MC_IN_PERCENT) WHERE (M1%R4  (:,:,:) .NE. M1%NODATA_VALUE  ) M1%R4  (:,:,:) = 0.01 * M1%R4  (:,:,:)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(6)) THEN
   FN = TRIM(WEATHER_DIRECTORY) // TRIM(M10_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (M10,FN)
   IF (DEAD_MC_IN_PERCENT) WHERE (M10%R4 (:,:,:) .NE. M10%NODATA_VALUE ) M10%R4 (:,:,:) = 0.01 * M10%R4 (:,:,:)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(7)) THEN
   FN = TRIM(WEATHER_DIRECTORY) // TRIM(M100_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (M100,FN)
   IF (DEAD_MC_IN_PERCENT) WHERE (M100%R4(:,:,:) .NE. M100%NODATA_VALUE) M100%R4(:,:,:) = 0.01 * M100%R4(:,:,:)
ENDIF

IF (USE_ERC .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(8) ) THEN
   FN = TRIM(WEATHER_DIRECTORY) // TRIM(ERC_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED(ERC,FN)
   CALL ERC_IGNITION_FACTOR (ERC, IGNFAC, 1, ERC%NBANDS)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(9)) THEN
   CONSTANT_LH  = -9999.
   IF (USE_CONSTANT_LH) THEN
      CONSTANT_LH = LH_MOISTURE_CONTENT
      IF (LIVE_MC_IN_PERCENT) CONSTANT_LH = CONSTANT_LH * 0.01
   ENDIF
   IF (CONSTANT_LH .GT. 0. ) THEN
      MLH%R4 (:,:,:) = CONSTANT_LH
   ELSE
      FN = TRIM(WEATHER_DIRECTORY) // TRIM(MLH_FILENAME)
      CALL READ_BSQ_RASTER_EXISTING_TILED(MLH,FN)
      IF (LIVE_MC_IN_PERCENT) MLH%R4(:,:,:) = 0.01 * MLH%R4(:,:,:)
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(10)) THEN
   CONSTANT_LW  = -9999.
   IF (USE_CONSTANT_LW) THEN
      CONSTANT_LW = LW_MOISTURE_CONTENT
      IF (LIVE_MC_IN_PERCENT) CONSTANT_LW = CONSTANT_LW * 0.01
   ENDIF
   IF (CONSTANT_LW .GT. 0. ) THEN
      MLW%R4 (:,:,:) = CONSTANT_LW
   ELSE
      FN = TRIM(WEATHER_DIRECTORY) // TRIM(MLW_FILENAME)
      CALL READ_BSQ_RASTER_EXISTING_TILED(MLW,FN)
      IF (LIVE_MC_IN_PERCENT) MLW%R4(:,:,:) = 0.01 * MLW%R4(:,:,:)
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(11)) THEN
   CONSTANT_FMC = -9999.
   IF (USE_CONSTANT_FMC) THEN
      CONSTANT_FMC = FOLIAR_MOISTURE_CONTENT
   ENDIF
   IF (CONSTANT_FMC .GT. 0. ) THEN
      MFOL%R4 (:,:,:) = CONSTANT_FMC
   ELSE
      FN = TRIM(WEATHER_DIRECTORY) // TRIM(FMC_FILENAME)
      CALL READ_BSQ_RASTER_EXISTING_TILED(MFOL,FN)
   ENDIF
ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
IF (NPROC .GT. 1) THEN
   CALL MPI_BCAST_RASTER_HEADER(WS   , PARALLEL_IO_RANK(3), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(WD   , PARALLEL_IO_RANK(4), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(M1   , PARALLEL_IO_RANK(5), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(M10  , PARALLEL_IO_RANK(6), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(M100 , PARALLEL_IO_RANK(7), .FALSE.)
   IF (USE_ERC) THEN
      CALL MPI_BCAST_RASTER_HEADER(ERC    , PARALLEL_IO_RANK(8), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(IGNFAC , PARALLEL_IO_RANK(8), .FALSE.)
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Reading fuel and topography rasters'

IF (USE_IGNITION_MASK .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(12)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(IGNITION_MASK_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (IGN_MASK,FN)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(13)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(ASP_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (ASP,FN)
   IF (TRIM(ASP%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      ASP%R4(:,:,1) = REAL(ASP%I2(:,:,1))
      DEALLOCATE(ASP%I2)
      ASP%PIXELTYPE='FLOAT     '
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(14)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(CBH_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (CBH,FN)
   IF (TRIM(CBH%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CBH%R4(:,:,1) = REAL(CBH%I2(:,:,1))
      DEALLOCATE(CBH%I2)
      CBH%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CBH_TIMES_10 ) WHERE(CBH%R4(:,:,1) .NE. CBH%NODATA_VALUE) CBH%R4(:,:,1) = 0.10*CBH%R4(:,:,1) ! m
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(15)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(CBD_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (CBD,FN)
   IF (TRIM(CBD%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CBD%R4(:,:,1) = REAL(CBD%I2(:,:,1))
      DEALLOCATE(CBD%I2)
      CBD%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CBD_TIMES_100) WHERE(CBD%R4(:,:,1) .NE. CBD%NODATA_VALUE) CBD%R4(:,:,1) = 0.01*CBD%R4(:,:,1) ! kg/m3
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(16)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(CC_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (CC,FN)
   IF (TRIM(CC%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CC%R4(:,:,1) = REAL(CC%I2(:,:,1))
      DEALLOCATE(CC%I2)
      CC%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CC_IN_PERCENT) WHERE(CC%R4 (:,:,1) .NE. CC%NODATA_VALUE ) CC%R4 (:,:,1) = 0.01*CC%R4 (:,:,1) ! -
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(17)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(CH_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (CH,FN)
   IF (TRIM(CH%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      CH%R4(:,:,1) = REAL(CH%I2(:,:,1))
      DEALLOCATE(CH%I2)
      CH%PIXELTYPE='FLOAT     '
   ENDIF
   IF (CH_TIMES_10  ) WHERE(CH%R4 (:,:,1) .NE. CH%NODATA_VALUE ) CH%R4 (:,:,1) = 0.10*CH%R4 (:,:,1) ! m
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(18) .AND. MODE .NE. 2) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(DEM_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (DEM, FN)
   IF (TRIM(DEM%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      DEM%R4(:,:,1)= REAL(DEM%I2(:,:,1))
      DEALLOCATE(DEM%I2)
      DEM%PIXELTYPE='FLOAT     '
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(19)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(FBFM_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (FBFM,FN)
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(20)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(SLP_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (SLP,FN)
   IF (TRIM(SLP%PIXELTYPE) .EQ. 'SIGNEDINT') THEN
      SLP%R4(:,:,1) = REAL(SLP%I2(:,:,1))
      DEALLOCATE(SLP%I2)
      SLP%PIXELTYPE='FLOAT     '
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(21) ) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(ADJ_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (ADJ, FN)
   WHERE (ADJ%R4 .LT. 0.) ADJ%R4 = 1.
ENDIF

IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(22) .AND. MODE .NE. 2) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(PHI_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (PHI0,FN)
   IF (NOISE) THEN
      DO IROW = 1, PHI0%NROWS
      DO ICOL = 1, PHI0%NCOLS
         CALL RANDOM_NUMBER(RX)
         PHI0%R4(ICOL,IROW,1) = PHI0%R4(ICOL,IROW,1) + 1E-3 * (0.5 - RX)
      ENDDO
      ENDDO
   ENDIF
ENDIF

IF (USE_POPULATION_DENSITY .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(23)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(POPULATION_DENSITY_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (POPULATION_DENSITY,FN)
ENDIF

IF (USE_REAL_ESTATE_VALUE  .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(24)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(REAL_ESTATE_VALUE_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (REAL_ESTATE_VALUE,FN)
ENDIF

IF (USE_LAND_VALUE         .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(25)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(LAND_VALUE_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (LAND_VALUE,FN)
ENDIF

IF (USE_SDI                .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(27)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(SDI_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (SDI,FN)
ENDIF

IF (USE_BLDG_SPREAD_MODEL) THEN
   IF (USE_CONSTANT_BLDG_SPREAD_MODEL_PARAMS) THEN
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(28)) BLDG_AREA%R4            (:,:,:) = BLDG_AREA_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(29)) BLDG_SEPARATION_DIST%R4 (:,:,:) = BLDG_SEPARATION_DIST_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(30)) BLDG_NONBURNABLE_FRAC%R4(:,:,:) = BLDG_NONBURNABLE_FRAC_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(31)) BLDG_FOOTPRINT_FRAC%R4  (:,:,:) = BLDG_FOOTPRINT_FRAC_CONSTANT
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(32)) BLDG_FUEL_MODEL%I2      (:,:,:) = BLDG_FUEL_MODEL_CONSTANT
   ELSE
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(28)) THEN
         FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(BLDG_AREA_FILENAME)
         CALL READ_BSQ_RASTER_EXISTING_TILED (BLDG_AREA,FN)
      ENDIF
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(29)) THEN
         FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(BLDG_SEPARATION_DIST_FILENAME)
         CALL READ_BSQ_RASTER_EXISTING_TILED (BLDG_SEPARATION_DIST,FN)
      ENDIF
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(30)) THEN
         FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(BLDG_NONBURNABLE_FRAC_FILENAME)
         CALL READ_BSQ_RASTER_EXISTING_TILED (BLDG_NONBURNABLE_FRAC,FN)
      ENDIF
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(31)) THEN
         FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(BLDG_FOOTPRINT_FRAC_FILENAME)
         CALL READ_BSQ_RASTER_EXISTING_TILED (BLDG_FOOTPRINT_FRAC,FN)
      ENDIF
      IF (IRANK_WORLD .EQ. PARALLEL_IO_RANK(32)) THEN
         FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(BLDG_FUEL_MODEL_FILENAME)
         CALL READ_BSQ_RASTER_EXISTING_TILED (BLDG_FUEL_MODEL,FN)
      ENDIF
   ENDIF
ENDIF

IF (USE_PYROMES .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(33)) THEN
   FN = TRIM(FUELS_AND_TOPOGRAPHY_DIRECTORY) // TRIM(PYROMES_FILENAME)
   CALL READ_BSQ_RASTER_EXISTING_TILED (PYROMES,FN)
ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
IF (NPROC .GT. 1) THEN
   IF (USE_IGNITION_MASK) CALL MPI_BCAST_RASTER_HEADER(IGN_MASK  , PARALLEL_IO_RANK(12), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(ASP       , PARALLEL_IO_RANK(13), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CBH       , PARALLEL_IO_RANK(14), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CBD       , PARALLEL_IO_RANK(15), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CC        , PARALLEL_IO_RANK(16), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(CH        , PARALLEL_IO_RANK(17), .FALSE.)
   IF (MODE .NE. 2) CALL MPI_BCAST_RASTER_HEADER(DEM  , PARALLEL_IO_RANK(18), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(FBFM      , PARALLEL_IO_RANK(19), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(SLP       , PARALLEL_IO_RANK(20), .FALSE.)
   CALL MPI_BCAST_RASTER_HEADER(ADJ       , PARALLEL_IO_RANK(21), .FALSE.)
   IF (MODE .NE. 2) CALL MPI_BCAST_RASTER_HEADER(PHI0 , PARALLEL_IO_RANK(22), .FALSE.)
   IF (USE_POPULATION_DENSITY) CALL MPI_BCAST_RASTER_HEADER(POPULATION_DENSITY , PARALLEL_IO_RANK(23), .FALSE.)
   IF (USE_REAL_ESTATE_VALUE ) CALL MPI_BCAST_RASTER_HEADER(REAL_ESTATE_VALUE   ,PARALLEL_IO_RANK(24), .FALSE.)
   IF (USE_LAND_VALUE        ) CALL MPI_BCAST_RASTER_HEADER(LAND_VALUE         , PARALLEL_IO_RANK(25), .FALSE.)
   IF (USE_SDI               ) CALL MPI_BCAST_RASTER_HEADER(SDI                , PARALLEL_IO_RANK(27), .FALSE.)
   IF (USE_BLDG_SPREAD_MODEL .AND. (.NOT. USE_CONSTANT_BLDG_SPREAD_MODEL_PARAMS)) THEN
      CALL MPI_BCAST_RASTER_HEADER(BLDG_AREA            , PARALLEL_IO_RANK(28), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_SEPARATION_DIST , PARALLEL_IO_RANK(29), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_NONBURNABLE_FRAC, PARALLEL_IO_RANK(30), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_FOOTPRINT_FRAC  , PARALLEL_IO_RANK(31), .FALSE.)
      CALL MPI_BCAST_RASTER_HEADER(BLDG_FUEL_MODEL      , PARALLEL_IO_RANK(32), .FALSE.)
   ENDIF
   IF (USE_PYROMES) CALL MPI_BCAST_RASTER_HEADER(PYROMES, PARALLEL_IO_RANK(33), .FALSE.)
ENDIF

! *****************************************************************************
END SUBROUTINE READ_WEATHER_FUEL_TOPOGRAPHY_TILED
! *****************************************************************************

! *****************************************************************************
RECURSIVE SUBROUTINE WRITE_BIL_RASTER(RASTER, INDIR, STUB, CONVERT_TO_GEOTIFF_LOCAL, COMPRESS, THREADNUM)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (RASTER_TYPE), INTENT(IN) :: RASTER
CHARACTER(400), INTENT(IN) :: INDIR,STUB
LOGICAL, INTENT(IN) :: CONVERT_TO_GEOTIFF_LOCAL,COMPRESS
INTEGER, INTENT(IN) :: THREADNUM
CHARACTER(400) :: SHELLSTR,FNBIL,FNHDR,FNTIF
INTEGER :: IOS, IROW, ICOL, IBAND, ISTAT, LUOUT
REAL, ALLOCATABLE, DIMENSION(:) ::  RVALUES
INTEGER :: ICOLSTART, ICOLSTOP, IROWSTART, IROWSTOP, IREC
INTEGER*8 :: LRECL
INTEGER*2, ALLOCATABLE, DIMENSION(:) :: I2VALUES

LUOUT = LUOUTPUT + THREADNUM

IF (TRIM(SCRATCH) .EQ. 'null') THEN
   FNTIF = TRIM(INDIR) // TRIM(STUB) // '.tif'
   FNHDR = TRIM(INDIR) // TRIM(STUB) // '.hdr'
   FNBIL = TRIM(INDIR) // TRIM(STUB) // '.bil'
ELSE
   FNTIF = TRIM(INDIR  ) // TRIM(STUB) // '.tif'
   FNHDR = TRIM(SCRATCH) // TRIM(STUB) // '.hdr'
   FNBIL = TRIM(SCRATCH) // TRIM(STUB) // '.bil'
ENDIF

IF (.NOT. CONVERT_TO_GEOTIFF_LOCAL) THEN
   FNHDR = TRIM(INDIR  ) // TRIM(STUB) // '.hdr'
   FNBIL = TRIM(INDIR  ) // TRIM(STUB) // '.bil'
ENDIF

ICOLSTART  = 1 
ICOLSTOP   = RASTER%NCOLS
IROWSTART  = 1
IROWSTOP   = RASTER%NROWS

!Open and write hdr file:
OPEN(LUOUT,FILE=TRIM(FNHDR),FORM='FORMATTED',STATUS='REPLACE',IOSTAT=IOS)

WRITE(LUOUT,110,IOSTAT=IOS) 'BYTEORDER      ', TRIM(RASTER%BYTEORDER)
WRITE(LUOUT,110,IOSTAT=IOS) 'LAYOUT         ', TRIM(RASTER%LAYOUT)
WRITE(LUOUT,300,IOSTAT=IOS) 'NROWS          ', RASTER%NROWS 
WRITE(LUOUT,300,IOSTAT=IOS) 'NCOLS          ', RASTER%NCOLS
WRITE(LUOUT,300,IOSTAT=IOS) 'NBANDS         ', RASTER%NBANDS
WRITE(LUOUT,300,IOSTAT=IOS) 'NBITS          ', RASTER%NBITS
WRITE(LUOUT,300,IOSTAT=IOS) 'BANDROWBYTES   ', RASTER%BANDROWBYTES  !4*RASTER%NCOLS
WRITE(LUOUT,300,IOSTAT=IOS) 'TOTALROWBYTES  ', RASTER%TOTALROWBYTES !4*RASTER%NBANDS*RASTER%NCOLS 
WRITE(LUOUT,110,IOSTAT=IOS) 'PIXELTYPE      ', TRIM(RASTER%PIXELTYPE)
WRITE(LUOUT,400,IOSTAT=IOS) 'ULXMAP         ', RASTER%ULXMAP
WRITE(LUOUT,400,IOSTAT=IOS) 'ULYMAP         ', RASTER%ULYMAP
WRITE(LUOUT,400,IOSTAT=IOS) 'XDIM           ', RASTER%CELLSIZE
WRITE(LUOUT,400,IOSTAT=IOS) 'YDIM           ', RASTER%CELLSIZE
WRITE(LUOUT,400,IOSTAT=IOS) 'NODATA         ', RASTER%NODATA_VALUE
CLOSE(LUOUT,IOSTAT=IOS)

!Open and write raster file:

SELECT CASE (TRIM(RASTER%PIXELTYPE))

   CASE ('FLOAT') 

      ALLOCATE(RVALUES(1:RASTER%NCOLS*RASTER%NBANDS))

      INQUIRE (IOLENGTH=LRECL) RVALUES(:)
      OPEN(LUOUT,FILE=TRIM(FNBIL),ACCESS='DIRECT',STATUS='REPLACE',RECL=LRECL,IOSTAT=IOS) 
      IF (IOS .GT. 0) THEN
         WRITE(*,*) 'Problem opening output bil file ', TRIM(FNBIL)
         STOP
      ENDIF

      IREC = 0
      DO IROW = IROWSTOP, IROWSTART, -1

         IREC = IREC + 1
         RVALUES(:) = REAL(RASTER%NODATA_VALUE)
   
         ICOLSTART = 1
         DO IBAND = 1, RASTER%NBANDS
            ICOLSTOP  = ICOLSTART + RASTER%NCOLS - 1
            RVALUES(ICOLSTART:ICOLSTOP) = RASTER%R4(1:RASTER%NCOLS,IROW,IBAND)   
            ICOLSTART = ICOLSTOP + 1
         ENDDO
   
         WRITE(LUOUT,REC=IREC,IOSTAT=IOS) (RVALUES(ICOL), ICOL=1, ICOLSTOP)
   
      ENDDO

!      FLUSH(LUOUT)
      CLOSE(LUOUT,IOSTAT=IOS)

      DEALLOCATE(RVALUES)

      IF (CONVERT_TO_GEOTIFF_LOCAL) THEN
         IF (COMPRESS) THEN
            SHELLSTR = TRIM(PATH_TO_GDAL) // 'gdal_translate -ot Float32 -co "COMPRESS=DEFLATE" -co "ZLEVEL=9" -a_srs "' // &
                       TRIM(A_SRS) // '" ' // TRIM(FNBIL) // ' ' // TRIM(FNTIF)
            WRITE(*,100) TRIM(SHELLSTR)
            ISTAT = SYSTEM(TRIM(SHELLSTR))
         ELSE
            SHELLSTR = TRIM(PATH_TO_GDAL) // 'gdal_translate -ot Float32 -a_srs "' // &
                       TRIM(A_SRS) // '" ' // TRIM(FNBIL) // ' '// TRIM(FNTIF)
            WRITE(*,100) TRIM(SHELLSTR)
            ISTAT = SYSTEM(TRIM(SHELLSTR))
         ENDIF

         SHELLSTR = TRIM(DELETECOMMAND) // " " // TRIM(FNBIL); WRITE(*,100) TRIM(SHELLSTR)
         ISTAT = SYSTEM(TRIM(SHELLSTR))

         SHELLSTR = TRIM(DELETECOMMAND) // " " // TRIM(FNHDR); WRITE(*,100) TRIM(SHELLSTR)
         ISTAT = SYSTEM(TRIM(SHELLSTR))

      ENDIF

   CASE ('SIGNEDINT') 

      ALLOCATE(I2VALUES(1:RASTER%NCOLS*RASTER%NBANDS))

      INQUIRE (IOLENGTH = LRECL) I2VALUES(:)
      OPEN(LUOUT,FILE=TRIM(FNBIL),ACCESS='DIRECT',STATUS='REPLACE',RECL=LRECL,IOSTAT=IOS) 
      IF (IOS .GT. 0) THEN
         WRITE(*,*) 'Problem opening output bil file ', TRIM(FNBIL)
         STOP
      ENDIF

      IREC = 0
      DO IROW = IROWSTOP, IROWSTART, -1

         IREC = IREC + 1
         I2VALUES(:) = NINT(RASTER%NODATA_VALUE,2)
   
         ICOLSTART = 1
         DO IBAND = 1, RASTER%NBANDS
            ICOLSTOP  = ICOLSTART + RASTER%NCOLS - 1
            I2VALUES(ICOLSTART:ICOLSTOP) = RASTER%I2(1:RASTER%NCOLS,IROW,IBAND)   
            ICOLSTART = ICOLSTOP + 1
         ENDDO
   
         WRITE(LUOUT,REC=IREC,IOSTAT=IOS) (I2VALUES(ICOL), ICOL=1, ICOLSTOP)
   
      ENDDO

      CLOSE(LUOUT,IOSTAT=IOS)

      DEALLOCATE(I2VALUES)

      IF (CONVERT_TO_GEOTIFF_LOCAL) THEN
         IF (COMPRESS) THEN
            SHELLSTR = TRIM(PATH_TO_GDAL) // 'gdal_translate -ot Int16 -co "COMPRESS=DEFLATE" -co "ZLEVEL=9" -a_srs "' // &
                       TRIM(A_SRS) // '" ' // TRIM(FNBIL) // ' ' // TRIM(FNTIF)
            WRITE(*,100) TRIM(SHELLSTR); ISTAT = SYSTEM(TRIM(SHELLSTR))
         ELSE
            SHELLSTR = TRIM(PATH_TO_GDAL) // 'gdal_translate -ot Int16 -a_srs "' // &
                       TRIM(A_SRS) // '" ' // TRIM(FNBIL) // ' ' // TRIM(FNTIF)
            WRITE(*,100) TRIM(SHELLSTR); ISTAT = SYSTEM(TRIM(SHELLSTR))   
         ENDIF

         SHELLSTR = TRIM(DELETECOMMAND) // " " // TRIM(FNBIL); WRITE(*,100) TRIM(SHELLSTR); ISTAT = SYSTEM(TRIM(SHELLSTR))
         SHELLSTR = TRIM(DELETECOMMAND) // " " // TRIM(FNHDR); WRITE(*,100) TRIM(SHELLSTR); ISTAT = SYSTEM(TRIM(SHELLSTR))
      ENDIF

   CASE DEFAULT
      WRITE(*,*) 'DEFAULT SO NOTHING HAPPENS'
      CONTINUE

END SELECT

100 FORMAT(A)
110 FORMAT(A,A)

300 FORMAT(A, I8)
400 FORMAT(A, F12.2)

! *****************************************************************************
END SUBROUTINE WRITE_BIL_RASTER
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_BSQ_XML_HEADER(RASTER,INDIR,FN,DELETE_INTERMEDIATE_FILES)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (RASTER_TYPE) :: RASTER
CHARACTER(400), INTENT (IN) :: INDIR,FN
LOGICAL, INTENT(IN) :: DELETE_INTERMEDIATE_FILES
CHARACTER(400) :: FNHDR, FNBSQ, FNTIF, FNPRJ, FNXML, SHELLSTR, TEMPSTR, LINENOW
CHARACTER(5) :: ISBANDS,ISLINES
CHARACTER(7) :: ISSAMPLES
CHARACTER(9) :: ISDATATYPE
CHARACTER(24) :: ISNODATA
CHARACTER(400), ALLOCATABLE, DIMENSION(:) :: LINES
INTEGER :: I, IOS, ISTAT, ITYPE, NLINES

IF (VRT_INSTEAD_OF_TIF) THEN
   FNTIF = TRIM(INDIR) // TRIM(FN) // '.vrt'
ELSE
   FNTIF = TRIM(INDIR) // TRIM(FN) // '.tif'
ENDIF

IF (TRIM(SCRATCH) .EQ. 'null') THEN
   FNHDR = TRIM(INDIR) // TRIM(FN) // '.hdr'
   FNBSQ = TRIM(INDIR) // TRIM(FN) // '.bsq'
   FNPRJ = TRIM(INDIR) // TRIM(FN) // '.prj'
   FNXML = TRIM(FNBSQ) // '.aux.xml'
ELSE
   FNHDR = TRIM(SCRATCH) // TRIM(FN) // '.hdr'
   FNBSQ = TRIM(SCRATCH) // TRIM(FN) // '.bsq'
   FNPRJ = TRIM(SCRATCH) // TRIM(FN) // '.prj'
   FNXML = TRIM(FNBSQ) // '.aux.xml'
ENDIF

! Convert to ENVI header BSQ format
IF (.NOT. USE_EXISTING_BSQS) THEN
   SHELLSTR = TRIM(PATH_TO_GDAL) // 'gdal_translate -of ENVI -co "INTERLEAVE=BSQ" ' // TRIM(FNTIF) // " " // TRIM(FNBSQ)
   WRITE(*,100) TRIM(SHELLSTR); CALL EXECUTE_COMMAND_LINE(TRIM(SHELLSTR))
ENDIF

! Open and parse BSQ XML:
OPEN(LUINPUT,FILE=TRIM(FNXML),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS) 
IF (IOS .GT. 0) THEN
   WRITE(*,*) 'Problem opening bsq xml header ', TRIM(FNXML)
   WRITE(*,*) 'IOS: ', IOS
   STOP
ENDIF

! Figure out number of lines
IOS=0
NLINES=0
DO WHILE (IOS .EQ. 0)
   READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
   IF (IOS .EQ. 0) NLINES = NLINES + 1
ENDDO
REWIND(LUINPUT)

! Now read in all lines
ALLOCATE(LINES(1:NLINES))
DO I = 1, NLINES
   READ(LUINPUT,100,IOSTAT=IOS) LINES(I)
ENDDO
CLOSE(LUINPUT)

DO I = 1, NLINES
   LINENOW    = LINES(I)
   ISBANDS    = LINENOW(15:19)
   ISDATATYPE = LINENOW(15:23)
   ISLINES    = LINENOW(15:19) ! rows
   ISSAMPLES  = LINENOW(15:21) ! columns
   ISNODATA   = LINENOW(6:16)
   CONTINUE
      
   IF (ISBANDS == 'bands') THEN
      READ(LINENOW(22:30),*) TEMPSTR
      TEMPSTR = STRIP_NON_NUMBERS(TEMPSTR)
      READ(TEMPSTR,*,IOSTAT=IOS) RASTER%NBANDS
      IF (IOS .NE. 0) THEN
         RASTER%NBANDS=1
      ENDIF
      CONTINUE
   ENDIF

   IF (ISDATATYPE == 'data_type') THEN
      READ(LINENOW(26:26),*) TEMPSTR
      READ(TEMPSTR,*) ITYPE
      IF (ITYPE .EQ. 2) THEN
         RASTER%PIXELTYPE='SIGNEDINT'
         RASTER%NBITS=16
      ENDIF
      IF (ITYPE .EQ. 4) THEN
         RASTER%PIXELTYPE='FLOAT'
         RASTER%NBITS=32
      ENDIF
   ENDIF

   IF (ISLINES == 'lines') THEN ! rows
      READ(LINENOW(22:30),*) TEMPSTR
      TEMPSTR = STRIP_NON_NUMBERS(TEMPSTR)
      READ(TEMPSTR,*) RASTER%NROWS
      CONTINUE
   ENDIF

   IF (ISSAMPLES == 'samples') THEN
      READ(LINENOW(24:32),*) TEMPSTR
      TEMPSTR = STRIP_NON_NUMBERS(TEMPSTR)
      READ(TEMPSTR,*) RASTER%NCOLS
      CONTINUE
   ENDIF

   IF (ISNODATA == 'NoDataValue') THEN
      READ(LINENOW(18:38),*) TEMPSTR
!      TEMPSTR = STRIP_NON_NUMBERS(TEMPSTR)
      READ(TEMPSTR,*) RASTER%NODATA_VALUE
      CONTINUE
   ENDIF

ENDDO

RASTER%BANDROWBYTES  = (RASTER%NBITS/8) * RASTER%NCOLS
RASTER%TOTALROWBYTES = RASTER%BANDROWBYTES * RASTER%NBANDS

IF (DELETE_INTERMEDIATE_FILES) THEN
   SHELLSTR = TRIM(DELETECOMMAND) // " " // TRIM(FNBSQ) // " " // TRIM(FNHDR) // " " // TRIM(FNPRJ) // " " // TRIM(FNXML)
   WRITE(*,100) TRIM(SHELLSTR); ISTAT = SYSTEM(TRIM(SHELLSTR))
ENDIF

DEALLOCATE(LINES)

100 FORMAT(A)

CONTAINS

CHARACTER(400) FUNCTION STRIP_NON_NUMBERS(STR)

   INTEGER :: I,J
   CHARACTER(400), INTENT(INOUT) :: STR
   CHARACTER :: NUMBERS(0:9)
   LOGICAL :: ISNUMBER
   
   DO J = 0, 9
      WRITE(NUMBERS(J), '(I1.1)') J
   ENDDO

   DO I = 1, 400
      ISNUMBER = .FALSE. 
      DO J = 0, 9
         IF (STR(I:I) .EQ. NUMBERS(J)) ISNUMBER = .TRUE.
         CONTINUE
      ENDDO
      IF (.NOT. ISNUMBER) STR(I:I) = ' '
   ENDDO

   STRIP_NON_NUMBERS = STR

END FUNCTION STRIP_NON_NUMBERS

! *****************************************************************************
END SUBROUTINE READ_BSQ_XML_HEADER
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_BSQ_HDR_HEADER(RASTER,INDIR,FN,DELETE_INTERMEDIATE_FILES)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (RASTER_TYPE) :: RASTER
CHARACTER(400), INTENT (IN) :: INDIR,FN
LOGICAL, INTENT(IN) :: DELETE_INTERMEDIATE_FILES
CHARACTER(400) :: FNHDR, FNBSQ, FNTIF, SHELLSTR
INTEGER :: ISTAT

! Parsed ENVI HDR variables
INTEGER :: SAMPLES, LINES, BANDS, DATA_TYPE, HEADER_OFFSET, BYTE_ORDER
REAL :: DATA_IGNORE_VALUE, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE
REAL :: EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE
CHARACTER(3) :: INTERLEAVE
CHARACTER(1024) :: COORDINATE_SYSTEM, DESCRIPTION
CHARACTER(1024) :: DEFAULT_BANDS, BAND_NAMES
CHARACTER(64) :: FILE_TYPE, PROJECTION_NAME

IF (VRT_INSTEAD_OF_TIF) THEN
   FNTIF = TRIM(INDIR) // TRIM(FN) // '.vrt'
ELSE
   FNTIF = TRIM(INDIR) // TRIM(FN) // '.tif'
ENDIF

IF (TRIM(SCRATCH) .EQ. 'null') THEN
   FNHDR = TRIM(INDIR) // TRIM(FN) // '.hdr'
   FNBSQ = TRIM(INDIR) // TRIM(FN) // '.bsq'
ELSE
   FNHDR = TRIM(SCRATCH) // TRIM(FN) // '.hdr'
   FNBSQ = TRIM(SCRATCH) // TRIM(FN) // '.bsq'
ENDIF

CALL PARSE_ENVI_HEADER(FNHDR, DESCRIPTION, SAMPLES, LINES, BANDS, HEADER_OFFSET, FILE_TYPE, DATA_TYPE, INTERLEAVE, BYTE_ORDER, BAND_NAMES, COORDINATE_SYSTEM, DATA_IGNORE_VALUE, DEFAULT_BANDS, PROJECTION_NAME, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE, EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE)

! These values should always be set
RASTER%NROWS = LINES
RASTER%NCOLS = SAMPLES
RASTER%NODATA_VALUE = DATA_IGNORE_VALUE
RASTER%XDIM = X_PIXEL_SIZE
RASTER%YDIM = Y_PIXEL_SIZE
RASTER%XLLCORNER = EASTING
RASTER%YLLCORNER = NORTHING - Y_PIXEL_SIZE * LINES
RASTER%LAYOUT = INTERLEAVE

SELECT CASE(TRIM(INTERLEAVE))
   CASE('BIL')
      RASTER%BYTEORDER = 'I'
   CASE('BSQ')
      RASTER%BYTEORDER = '0'
END SELECT


SELECT CASE(DATA_TYPE)
   CASE(2)
      RASTER%PIXELTYPE='SIGNEDINT'
      RASTER%NBITS=16
   CASE(4)
      RASTER%PIXELTYPE='FLOAT'
      RASTER%NBITS=32
END SELECT

! Check to make sure x and y cell sizes are the same:
IF (RASTER%XDIM .NE. RASTER%YDIM) THEN
   WRITE(*,*) 'Error opening ', TRIM(FNHDR), ' because XDIM is not equal to YDIM.'
   STOP
ELSE
   RASTER%CELLSIZE=RASTER%XDIM
ENDIF

! Need to handle some defaults
IF (BANDS .LT. 0) THEN
   RASTER%NBANDS = 1
ELSE
   RASTER%NBANDS = BANDS
ENDIF

! Set BANDROWBYTES and TOTALROWBYTES:
RASTER%BANDROWBYTES  = (RASTER%NBITS/8) * RASTER%NCOLS
RASTER%TOTALROWBYTES = RASTER%BANDROWBYTES * RASTER%NBANDS

! Set ULXMAP and ULYMAP
RASTER%ULXMAP = RASTER%XLLCORNER + 0.5 * RASTER%CELLSIZE
RASTER%ULYMAP = RASTER%YLLCORNER + RASTER%NROWS * RASTER%CELLSIZE - 0.5 * RASTER%CELLSIZE

IF (DELETE_INTERMEDIATE_FILES) THEN
   SHELLSTR = TRIM(DELETECOMMAND) // " " // TRIM(FNBSQ) // " " // TRIM(FNHDR)
   WRITE(*,100) TRIM(SHELLSTR); ISTAT = SYSTEM(TRIM(SHELLSTR))
ENDIF

100 FORMAT(A)

! *****************************************************************************
END SUBROUTINE READ_BSQ_HDR_HEADER
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_BSQ_HEADER_EXISTING_TILED(RASTER,FNIN)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (RASTER_TYPE), INTENT(OUT) :: RASTER
CHARACTER(400), INTENT (IN) :: FNIN
CHARACTER(400) :: FNHDR, FNBSQ, FNXML, TEMPSTR
INTEGER :: I, IOS, ITYPE, ITILE, JTILE
CHARACTER(1), DIMENSION(1:3), PARAMETER :: CINDEX = (/'1', '2', '3'/)

ITILE = 1
JTILE = 1

FNHDR = TRIM(FNIN) // '_' // CINDEX(ITILE) // '_' // CINDEX(JTILE) // '.hdr'
FNBSQ = TRIM(FNIN) // '_' // CINDEX(ITILE) // '_' // CINDEX(JTILE) // '.bsq'
FNXML = TRIM(FNBSQ) // '.aux.xml'

! Open and parse BSQ XML:
OPEN(LUINPUT,FILE=TRIM(FNXML),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS) 
IF (IOS .GT. 0) THEN
   WRITE(*,*) 'Problem opening bsq xml header ', TRIM(FNXML)
   WRITE(*,*) 'IOS: ', IOS
   STOP
ENDIF

! Trash first five lines
DO I = 1, 5
   READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
   CONTINUE
ENDDO

! Read number of bands:
READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
READ(TEMPSTR(22:30),*) TEMPSTR
TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
READ(TEMPSTR,*,IOSTAT=IOS) RASTER%NBANDS

IF (IOS .NE. 0) RASTER%NBANDS=1

! Skip over byte order:
READ(LUINPUT,100,IOSTAT=IOS)

! Read data type
READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
READ(TEMPSTR(26:26),*) TEMPSTR
READ(TEMPSTR,*) ITYPE
IF (ITYPE .EQ. 2) THEN
   RASTER%PIXELTYPE='SIGNEDINT'
   RASTER%NBITS=16
ENDIF
IF (ITYPE .EQ. 4) THEN
   RASTER%PIXELTYPE='FLOAT'
   RASTER%NBITS=32
ENDIF

! Skip 3 more lines
DO I = 1, 3
   READ(LUINPUT,100,IOSTAT=IOS)
ENDDO

! Read number of rows:
READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
READ(TEMPSTR(22:30),*) TEMPSTR
TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
READ(TEMPSTR,*) RASTER%NROWS

! Read number of columns:
READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
READ(TEMPSTR(24:32),*) TEMPSTR
TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
READ(TEMPSTR,*) RASTER%NCOLS

CLOSE(LUINPUT,IOSTAT=IOS)

!RASTER%XLLCORNER = RASTER%ULXMAP - 0.5 * RASTER%CELLSIZE
!RASTER%YLLCORNER = RASTER%ULYMAP + 0.5 * RASTER%CELLSIZE - REAL(RASTER%NROWS) * RASTER%CELLSIZE 

RASTER%NCOLS         = RASTER%NCOLS * 3
RASTER%NROWS         = RASTER%NROWS * 3
RASTER%BANDROWBYTES  = (RASTER%NBITS/8) * RASTER%NCOLS
RASTER%TOTALROWBYTES = RASTER%BANDROWBYTES * RASTER%NBANDS

100 FORMAT(A)

! *****************************************************************************
END SUBROUTINE READ_BSQ_HEADER_EXISTING_TILED
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_BSQ_RASTER(RASTER,INDIR,FN)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (RASTER_TYPE) :: RASTER
CHARACTER(400), INTENT (IN) :: INDIR,FN
CHARACTER(400) :: FNHDR, FNBSQ, FNTIF, TEMPSTR, SHELLSTR
INTEGER :: I, IOS, IBAND, IROW1, IROW2, IDUMMY, J, IBANDCOUNT, NBANDS
INTEGER*8 :: LRECL
LOGICAL :: FOUND, HDR_EXISTS, BSQ_EXISTS
REAL, ALLOCATABLE, DIMENSION(:) :: RVALUES
REAL, ALLOCATABLE, DIMENSION(:,:) :: RTEMP
INTEGER*2, ALLOCATABLE, DIMENSION(:) :: I2VALUES
INTEGER*2, ALLOCATABLE, DIMENSION(:,:) :: I2TEMP

IF (VRT_INSTEAD_OF_TIF) THEN
   FNTIF = TRIM(INDIR) // TRIM(FN) // '.vrt'
ELSE
   FNTIF = TRIM(INDIR) // TRIM(FN) // '.tif'
ENDIF

IF (TRIM(SCRATCH) .EQ. 'null') THEN
   FNHDR = TRIM(INDIR) // TRIM(FN) // '.hdr'
   FNBSQ = TRIM(INDIR) // TRIM(FN) // '.bsq'
ELSE
   FNHDR = TRIM(SCRATCH) // TRIM(FN) // '.hdr'
   FNBSQ = TRIM(SCRATCH) // TRIM(FN) // '.bsq'
ENDIF

! Convert to ENVI header BSQ format if files don't already exist
INQUIRE(FILE=TRIM(FNHDR),EXIST=HDR_EXISTS)
INQUIRE(FILE=TRIM(FNBSQ),EXIST=BSQ_EXISTS)
IF (.NOT. HDR_EXISTS .OR. .NOT. BSQ_EXISTS) THEN
   SHELLSTR = TRIM(PATH_TO_GDAL) // 'gdal_translate -of ENVI -co "INTERLEAVE=BSQ" ' // TRIM(FNTIF) // " " // TRIM(FNBSQ)
   WRITE(*,100) TRIM(SHELLSTR); CALL EXECUTE_COMMAND_LINE(TRIM(SHELLSTR))
ENDIF

IF (USE_BSQ_XML_HEADER) THEN
   CALL READ_BSQ_XML_HEADER (RASTER , INDIR, FN, .FALSE.)

! Now open and parse BSQ .hdr file:
   OPEN(LUINPUT,FILE=TRIM(FNHDR),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS) 
   IF (IOS .GT. 0) THEN
      WRITE(*,*) 'Problem opening bsq header ', TRIM(FNHDR)
   ENDIF

   IF (DEBUG_LEVEL .GT. 100) WRITE(*,*) 'Reading bsq header'

! Skip 11 lines
   DO I = 1, 11
      READ(LUINPUT,100,IOSTAT=IOS)
   ENDDO

! Read easting, northing, and cell size info:
   READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
   FOUND=.FALSE.; I=1
   DO WHILE (.NOT. FOUND)
      IF(TEMPSTR(I:I) .EQ. ',') THEN
         FOUND=.TRUE.
      ELSE
         I=I+1
      ENDIF
   ENDDO
   J=I+1
   DO I=1,100
      IF (TEMPSTR(I:I) .EQ. '}') TEMPSTR(I:I)=' '
   ENDDO
   READ(TEMPSTR(J:),*) IDUMMY, IDUMMY, RASTER%XLLCORNER, RASTER%YLLCORNER, RASTER%XDIM, RASTER%YDIM
   RASTER%YLLCORNER = RASTER%YLLCORNER - RASTER%YDIM * RASTER%NROWS
   CLOSE(LUINPUT)

! Check to make sure x and y cell sizes are the same:
   IF (RASTER%XDIM .NE. RASTER%YDIM) THEN
      WRITE(*,*) 'Error opening ', TRIM(FNHDR), ' because XDIM is not equal to YDIM.'
      STOP
   ELSE
      RASTER%CELLSIZE=RASTER%XDIM
   ENDIF

! Set ULXMAP and ULYMAP
   RASTER%ULXMAP = RASTER%XLLCORNER + 0.5 * RASTER%CELLSIZE
   RASTER%ULYMAP = RASTER%YLLCORNER + RASTER%NROWS * RASTER%CELLSIZE - 0.5 * RASTER%CELLSIZE

! Set BYTEORDER and LAYOUT
   RASTER%BYTEORDER = '0'
   RASTER%LAYOUT    = 'BSQ'

! This is for bil:
   RASTER%BYTEORDER = 'I'
   RASTER%LAYOUT    = 'BIL'
ELSE
   CALL READ_BSQ_HDR_HEADER(RASTER, INDIR, FN, .FALSE.)
ENDIF

IF (CSV_FIXED_IGNITION_LOCATIONS .AND. ONLY_READ_NEEDED_WX_BANDS .AND. RASTER%NBANDS .GT. 1) THEN
   NBANDS = 1 + IGN_IWX_BAND_HI - IGN_IWX_BAND_LO
ELSE
   NBANDS = RASTER%NBANDS
ENDIF

SELECT CASE(TRIM(RASTER%PIXELTYPE))

   CASE('FLOAT')
!     Allocate arrays as appropriate:
      IF (.NOT. ASSOCIATED(RASTER%R4)) ALLOCATE(RASTER%R4(1:RASTER%NCOLS,1:RASTER%NROWS,1:NBANDS))
      ALLOCATE(RVALUES(1:RASTER%NROWS*RASTER%NCOLS))
      ALLOCATE(RTEMP(1:RASTER%NCOLS,1:RASTER%NROWS))

!Open raster bsq file:
      INQUIRE (IOLENGTH=LRECL) RVALUES(:) 

      OPEN(LUINPUT, FILE=TRIM(FNBSQ), ACCESS='DIRECT', STATUS='OLD', RECL=LRECL, IOSTAT=IOS) 
      IF (IOS .GT. 0) THEN
         WRITE(*,*) 'Problem opening raster file ', TRIM(FNBSQ)
         STOP
      ENDIF

      IF (CSV_FIXED_IGNITION_LOCATIONS .AND. ONLY_READ_NEEDED_WX_BANDS) THEN
         IBANDCOUNT = 0
         DO IBAND = 1, RASTER%NBANDS
            IF (IBAND .LT. IGN_IWX_BAND_LO .AND. RASTER%NBANDS .GT. 1) CYCLE
            IF (IBAND .GT. IGN_IWX_BAND_HI .AND. RASTER%NBANDS .GT. 1) CYCLE
            IBANDCOUNT = IBANDCOUNT + 1

            READ(LUINPUT,REC=IBAND,IOSTAT=IOS) RVALUES(:)
            RTEMP(:,:) = RESHAPE(RVALUES, (/RASTER%NCOLS,RASTER%NROWS/))
            DO IROW1 = 1, RASTER%NROWS
               IROW2 = RASTER%NROWS + 1 - IROW1 
               RASTER%R4(:,IROW1,IBANDCOUNT) = RTEMP(:,IROW2)
            ENDDO
         ENDDO
         RASTER%NBANDS = NBANDS
      ELSE
         DO IBAND = 1, RASTER%NBANDS
            READ(LUINPUT,REC=IBAND,IOSTAT=IOS) RVALUES(:)
            RTEMP(:,:) = RESHAPE(RVALUES, (/RASTER%NCOLS,RASTER%NROWS/))
            DO IROW1 = 1, RASTER%NROWS
               IROW2 = RASTER%NROWS + 1 - IROW1 
               RASTER%R4(:,IROW1,IBAND) = RTEMP(:,IROW2)
            ENDDO
         ENDDO
      ENDIF

      DEALLOCATE(RVALUES, RTEMP)

   CASE('SIGNEDINT')
!     Allocate arrays as appropriate:
      IF (.NOT. ASSOCIATED(RASTER%I2)) ALLOCATE(RASTER%I2(1:RASTER%NCOLS,1:RASTER%NROWS,1:RASTER%NBANDS) )
      ALLOCATE(I2VALUES(1:RASTER%NROWS*RASTER%NCOLS))
      ALLOCATE(I2TEMP(1:RASTER%NCOLS,1:RASTER%NROWS))

!Open raster bsq file:
      INQUIRE (IOLENGTH=LRECL) I2VALUES(:) 

      OPEN(LUINPUT, FILE=TRIM(FNBSQ), ACCESS='DIRECT', STATUS='OLD', RECL=LRECL, IOSTAT=IOS) 
      IF (IOS .GT. 0) THEN
         WRITE(*,*) 'Problem opening raster file ', TRIM(FNBSQ)
         STOP
      ENDIF

      DO IBAND = 1, RASTER%NBANDS
         READ(LUINPUT,REC=IBAND,IOSTAT=IOS) I2VALUES(:)
         I2TEMP(:,:) = RESHAPE(I2VALUES, (/RASTER%NCOLS,RASTER%NROWS/))

         DO IROW1 = 1, RASTER%NROWS
            IROW2 = RASTER%NROWS + 1 - IROW1 
            RASTER%I2(:,IROW1,IBAND) = I2TEMP(:,IROW2)
         ENDDO

      ENDDO

      DEALLOCATE(I2VALUES, I2TEMP)

   CASE DEFAULT
      CONTINUE

END SELECT

CLOSE(LUINPUT,IOSTAT=IOS)

100 FORMAT(A)

! *****************************************************************************
END SUBROUTINE READ_BSQ_RASTER
! *****************************************************************************

! *****************************************************************************
SUBROUTINE READ_BSQ_RASTER_EXISTING_TILED(RASTER,FNIN)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (RASTER_TYPE), INTENT(INOUT) :: RASTER
CHARACTER(400), INTENT (IN) :: FNIN
CHARACTER(400) :: FNHDR, FNBSQ, FNXML, TEMPSTR
INTEGER :: I, J, IOS, IBAND, ITYPE, ITILE, JTILE
INTEGER :: IROW_BIG, IROW_SMALL, ICOL_BIG_LO, ICOL_BIG_HI, IROW_BIG_LO, IROW_BIG_HI, IDUMMY
INTEGER*8 :: LRECL
LOGICAL :: FOUND
REAL, ALLOCATABLE, DIMENSION(:) :: RVALUES
REAL, ALLOCATABLE, DIMENSION(:,:) :: RTEMP

INTEGER*2, ALLOCATABLE, DIMENSION(:) :: I2VALUES
INTEGER*2, ALLOCATABLE, DIMENSION(:,:) :: I2TEMP

CHARACTER(1), DIMENSION(1:3), PARAMETER :: CINDEX = (/'1', '2', '3'/)

DO ITILE = 1, 3
DO JTILE = 1, 3

   FNHDR = TRIM(FNIN) // '_' // CINDEX(ITILE) // '_' // CINDEX(JTILE) // '.hdr'
   FNBSQ = TRIM(FNIN) // '_' // CINDEX(ITILE) // '_' // CINDEX(JTILE) // '.bsq'
   FNXML = TRIM(FNBSQ) // '.aux.xml'

!Open and parse bsq header:
   IF (ITILE .EQ. 1. .AND. JTILE .EQ. 1) THEN

      OPEN(LUINPUT,FILE=TRIM(FNXML),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS) 
      IF (IOS .GT. 0) THEN
         WRITE(*,*) 'Problem opening bsq xml header ', TRIM(FNXML)
         WRITE(*,*) 'IOS: ', IOS
         STOP
      ENDIF

! Trash first five lines
      DO I = 1, 5
         READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
         CONTINUE
      ENDDO

! Read number of bands:
      READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      READ(TEMPSTR(22:30),*) TEMPSTR
      TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
      READ(TEMPSTR,*,IOSTAT=IOS) RASTER%NBANDS

      IF (IOS .NE. 0) THEN
         RASTER%NBANDS=1
      ENDIF

! Skip over byte order:
      READ(LUINPUT,100,IOSTAT=IOS)

! Read data type
      READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      READ(TEMPSTR(26:26),*) TEMPSTR
      READ(TEMPSTR,*) ITYPE
      IF (ITYPE .EQ. 2) THEN
         RASTER%PIXELTYPE='SIGNEDINT'
         RASTER%NBITS=16
      ENDIF
      IF (ITYPE .EQ. 4) THEN
         RASTER%PIXELTYPE='FLOAT'
         RASTER%NBITS=32
      ENDIF

! Skip 3 more lines
      DO I = 1, 3
         READ(LUINPUT,100,IOSTAT=IOS)
      ENDDO

! Read number of rows:
      READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      READ(TEMPSTR(22:30),*) TEMPSTR
      TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
      READ(TEMPSTR,*) RASTER%NROWS

! Read number of columns:
      READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      READ(TEMPSTR(24:32),*) TEMPSTR
      TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
      READ(TEMPSTR,*) RASTER%NCOLS

! Set BANDROWBYTES and TOTALROWBYTES:
      RASTER%BANDROWBYTES  = (RASTER%NBITS/8) * RASTER%NCOLS
      RASTER%TOTALROWBYTES = RASTER%BANDROWBYTES * RASTER%NBANDS

! Skip 5 or 6 more lines
      DO I = 1, 5
         READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      ENDDO

      IF (LEN_TRIM(TEMPSTR) .LT. 15) THEN !This checks to see if we need to trash one more line
         READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      ENDIF

! Read nodata:
      READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      READ(TEMPSTR(18:41),*) TEMPSTR
      TEMPSTR=TEMPSTR(1:LEN_TRIM(TEMPSTR)-1)
      READ(TEMPSTR,*) RASTER%NODATA_VALUE

! Close .xml file:
      CLOSE(LUINPUT,IOSTAT=IOS)

! Open and parse BSQ header:
      OPEN(LUINPUT,FILE=TRIM(FNHDR),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS) 
      IF (IOS .GT. 0) THEN
         WRITE(*,*) 'Problem opening bsq header ', TRIM(FNHDR)
      ENDIF

      IF (DEBUG_LEVEL .GT. 100) WRITE(*,*) 'Reading bsq header'

! Skip 11 lines
      DO I = 1, 11
         READ(LUINPUT,100,IOSTAT=IOS)
      ENDDO

! Read easting, northing, and cell size info:
      READ(LUINPUT,100,IOSTAT=IOS) TEMPSTR
      FOUND=.FALSE.; I=1
      DO WHILE (.NOT. FOUND)
         IF(TEMPSTR(I:I) .EQ. ',') THEN
            FOUND=.TRUE.
         ELSE
            I=I+1
         ENDIF
      ENDDO
      J=I+1
      DO I=1,130
         IF (TEMPSTR(I:I) .EQ. '}') TEMPSTR(I:I)=' '
      ENDDO
      READ(TEMPSTR(J:),*) IDUMMY, IDUMMY, RASTER%XLLCORNER, RASTER%YLLCORNER, RASTER%XDIM, RASTER%YDIM
      RASTER%YLLCORNER = RASTER%YLLCORNER - RASTER%YDIM * RASTER%NROWS
      CLOSE(LUINPUT)

! Check to make sure x and y cell sizes are the same:
      IF (RASTER%XDIM .NE. RASTER%YDIM) THEN
         WRITE(*,*) 'Error opening ', TRIM(FNHDR), ' because XDIM is not equal to YDIM.'
         STOP
      ELSE
         RASTER%CELLSIZE=RASTER%XDIM
      ENDIF

! Set ULXMAP and ULYMAP
      RASTER%ULXMAP = RASTER%XLLCORNER + 0.5 * RASTER%CELLSIZE
      RASTER%ULYMAP = RASTER%YLLCORNER + RASTER%NROWS * RASTER%CELLSIZE - 0.5 * RASTER%CELLSIZE

! This is for bil (because file is dumped as bil):
      RASTER%BYTEORDER = 'I'
      RASTER%LAYOUT    = 'BIL'

      SELECT CASE(TRIM(RASTER%PIXELTYPE))

         CASE('FLOAT')
            IF (.NOT. ASSOCIATED(RASTER%R4)) THEN
               ALLOCATE(RASTER%R4(1:3*RASTER%NCOLS,1:3*RASTER%NROWS,1:RASTER%NBANDS) )
               RASTER%R4(:,:,:) = RASTER%NODATA_VALUE
            ENDIF

            ALLOCATE(RVALUES(1:RASTER%NROWS*RASTER%NCOLS  ))
            ALLOCATE(RTEMP  (1:RASTER%NCOLS,1:RASTER%NROWS))

         CASE('SIGNEDINT')
            IF (.NOT. ASSOCIATED(RASTER%I2)) THEN
               ALLOCATE(RASTER%I2(1:3*RASTER%NCOLS,1:3*RASTER%NROWS,1:RASTER%NBANDS) )
               RASTER%I2(:,:,:) = NINT(RASTER%NODATA_VALUE,2)
            ENDIF

            ALLOCATE(I2VALUES(1:RASTER%NROWS*RASTER%NCOLS  ))
            ALLOCATE(I2TEMP  (1:RASTER%NCOLS,1:RASTER%NROWS))

      END SELECT

   ENDIF ! ITILE .EQ. 1. .AND. JTILE .EQ. 1

   ICOL_BIG_LO = (ITILE - 1) * RASTER%NCOLS + 1
   ICOL_BIG_HI = ICOL_BIG_LO + RASTER%NCOLS - 1

   IROW_BIG_LO = (JTILE - 1) * RASTER%NROWS + 1
   IROW_BIG_HI = IROW_BIG_LO + RASTER%NROWS - 1

   SELECT CASE(TRIM(RASTER%PIXELTYPE))

      CASE('FLOAT')

         INQUIRE (IOLENGTH=LRECL) RVALUES(:)

         OPEN(LUINPUT, FILE=TRIM(FNBSQ), ACCESS='DIRECT', STATUS='OLD', RECL=LRECL, IOSTAT=IOS) 
         IF (IOS .GT. 0) THEN
            WRITE(*,*) 'Problem opening raster file ', TRIM(FNBSQ)
            STOP
         ENDIF

         DO IBAND = 1, RASTER%NBANDS
            READ(LUINPUT,REC=IBAND,IOSTAT=IOS) RVALUES(:)
            RTEMP(:,:) = RESHAPE(RVALUES, (/RASTER%NCOLS,RASTER%NROWS/))

            IROW_SMALL = RASTER%NROWS
            DO IROW_BIG = IROW_BIG_LO, IROW_BIG_HI
               RASTER%R4(ICOL_BIG_LO:ICOL_BIG_HI,IROW_BIG,IBAND) = RTEMP(:,IROW_SMALL)
               IROW_SMALL = IROW_SMALL - 1
            ENDDO
         ENDDO

      CASE('SIGNEDINT')

         INQUIRE (IOLENGTH=LRECL) I2VALUES(:) 

         OPEN(LUINPUT, FILE=TRIM(FNBSQ), ACCESS='DIRECT', STATUS='OLD', RECL=LRECL, IOSTAT=IOS) 
         IF (IOS .GT. 0) THEN
            WRITE(*,*) 'Problem opening raster file ', TRIM(FNBSQ)
            STOP
         ENDIF

         DO IBAND = 1, RASTER%NBANDS
            READ(LUINPUT,REC=IBAND,IOSTAT=IOS) I2VALUES(:)
            I2TEMP(:,:) = RESHAPE(I2VALUES, (/RASTER%NCOLS,RASTER%NROWS/))

            IROW_SMALL = RASTER%NROWS
            DO IROW_BIG = IROW_BIG_LO, IROW_BIG_HI
               RASTER%I2(ICOL_BIG_LO:ICOL_BIG_HI,IROW_BIG,IBAND) = I2TEMP(:,IROW_SMALL)
               IROW_SMALL = IROW_SMALL - 1
            ENDDO
         ENDDO

   END SELECT

   CLOSE(LUINPUT,IOSTAT=IOS)

ENDDO
ENDDO

RASTER%ULYMAP = RASTER%ULYMAP + 2 * REAL(RASTER%NROWS) * RASTER%CELLSIZE 

RASTER%NCOLS         = RASTER%NCOLS * 3
RASTER%NROWS         = RASTER%NROWS * 3
RASTER%BANDROWBYTES  = (RASTER%NBITS/8) * RASTER%NCOLS
RASTER%TOTALROWBYTES = RASTER%BANDROWBYTES * RASTER%NBANDS

CONTINUE

SELECT CASE(TRIM(RASTER%PIXELTYPE))
   CASE('FLOAT')
      DEALLOCATE(RVALUES)
      DEALLOCATE(RTEMP)
   CASE('SIGNEDINT')
      DEALLOCATE(I2VALUES)
      DEALLOCATE(I2TEMP)
END SELECT

100 FORMAT(A)

! *****************************************************************************
END SUBROUTINE READ_BSQ_RASTER_EXISTING_TILED
! *****************************************************************************

! *****************************************************************************
SUBROUTINE LL_DUMP_ROUTINE(L,PREFIX,TIMENOW,QUANTITY,ICASE)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

TYPE (DLL), INTENT(INOUT) :: L
CHARACTER(*), INTENT(IN) :: PREFIX,QUANTITY
REAL, INTENT(IN) :: TIMENOW
INTEGER, INTENT(IN) :: ICASE

INTEGER :: I,IT
REAL :: ULXMAP,ULYMAP

CHARACTER(7) :: SEVEN
CHARACTER(7) :: CTSEC
CHARACTER(400) :: FN

TYPE(RASTER_TYPE), SAVE :: RDUMPME
TYPE(RASTER_TYPE), POINTER :: R
TYPE(NODE), POINTER :: C

C => L%HEAD
DO I = 1, L%NUM_NODES
   SELECT CASE (TRIM(QUANTITY))
      CASE ('time_suppressed')
          C%DUMPME = C%TIME_SUPPRESSED
      CASE ('time_added')
          C%DUMPME = C%TIME_ADDED
      CASE ('time_of_arrival')
          C%DUMPME = C%TIME_OF_ARRIVAL
      CASE DEFAULT
         WRITE(*,*) 'Quantity ', TRIM(QUANTITY), 'not found'
   END SELECT
   C => C%NEXT
ENDDO

IF (.NOT. ASSOCIATED(RDUMPME%R4)) THEN
   R=>ASP
   CALL ALLOCATE_EMPTY_RASTER(RDUMPME,  R%NCOLS, R%NROWS, 1, R%XLLCORNER, R%YLLCORNER, R%CELLSIZE, R%NODATA_VALUE, 'FLOAT     ')
ENDIF
   
WRITE(SEVEN, '(I7.7)') ICASE

IT=NINT(TIMENOW)
WRITE(CTSEC,'(I7)') IT
IF (IT .GE.       0 .AND. IT .LT.       10) CTSEC = '000000' // ADJUSTL(CTSEC) 
IF (IT .GE.      10 .AND. IT .LT.      100) CTSEC = '00000'  // ADJUSTL(CTSEC) 
IF (IT .GE.     100 .AND. IT .LT.     1000) CTSEC = '0000'   // ADJUSTL(CTSEC) 
IF (IT .GE.    1000 .AND. IT .LT.    10000) CTSEC = '000'    // ADJUSTL(CTSEC) 
IF (IT .GE.   10000 .AND. IT .LT.   100000) CTSEC = '00'     // ADJUSTL(CTSEC) 
IF (IT .GE.  100000 .AND. IT .LT.  1000000) CTSEC = '0'      // ADJUSTL(CTSEC) 

ULXMAP = ANALYSIS_XLLCORNER + 0.5 * ANALYSIS_CELLSIZE
ULYMAP = ANALYSIS_YLLCORNER + REAL(ANALYSIS_NROWS) * ANALYSIS_CELLSIZE - 0.5*ANALYSIS_CELLSIZE
RDUMPME%ULXMAP=ULXMAP; RDUMPME%ULYMAP=ULYMAP

RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
C => L%HEAD
DO I = 1, L%NUM_NODES
   RDUMPME%R4(C%IX,C%IY,1) = C%DUMPME
   C => C%NEXT
ENDDO
FN = TRIM(PREFIX) // '_' // SEVEN // '_' // CTSEC
CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,.FALSE.,.FALSE.,IRANK_WORLD)

! *****************************************************************************
END SUBROUTINE LL_DUMP_ROUTINE
! *****************************************************************************

! *****************************************************************************
SUBROUTINE MAIN_DUMP_ROUTINE(IDUMPCOUNT, NDUMPS, ICASE, T, ACRES)
! *****************************************************************************

#ifdef __INTEL_COMPILER
USE IFPORT
#endif

INTEGER, INTENT(INOUT) :: IDUMPCOUNT
INTEGER, INTENT(IN) :: NDUMPS, ICASE
REAL, INTENT(IN) :: T, ACRES
INTEGER :: I, IT, IOS
LOGICAL :: LOPEN

CHARACTER(7) :: SEVEN, CTSEC
CHARACTER(400) :: FN

REAL :: ULXMAP,ULYMAP

TYPE(RASTER_TYPE), SAVE :: I2DUMPME, RDUMPME
TYPE(RASTER_TYPE), POINTER :: R
TYPE(NODE), POINTER :: C

IF (.NOT. ASSOCIATED(I2DUMPME%I2)) THEN
   R=>ASP
   CALL ALLOCATE_EMPTY_RASTER(I2DUMPME, R%NCOLS, R%NROWS, 1, R%XLLCORNER, R%YLLCORNER, R%CELLSIZE, R%NODATA_VALUE, 'SIGNEDINT ')
   CALL ALLOCATE_EMPTY_RASTER(RDUMPME,  R%NCOLS, R%NROWS, 1, R%XLLCORNER, R%YLLCORNER, R%CELLSIZE, R%NODATA_VALUE, 'FLOAT     ')
ENDIF

IF (IDUMPCOUNT .GT. NDUMPS) RETURN
   
WRITE(SEVEN, '(I7.7)') STATS_ICASE(ICASE)

IT=NINT(T)
WRITE(CTSEC,'(I7)') IT
IF (IT .GE.       0 .AND. IT .LT.       10) CTSEC = '000000' // ADJUSTL(CTSEC) 
IF (IT .GE.      10 .AND. IT .LT.      100) CTSEC = '00000'  // ADJUSTL(CTSEC) 
IF (IT .GE.     100 .AND. IT .LT.     1000) CTSEC = '0000'   // ADJUSTL(CTSEC) 
IF (IT .GE.    1000 .AND. IT .LT.    10000) CTSEC = '000'    // ADJUSTL(CTSEC) 
IF (IT .GE.   10000 .AND. IT .LT.   100000) CTSEC = '00'     // ADJUSTL(CTSEC) 
IF (IT .GE.  100000 .AND. IT .LT.  1000000) CTSEC = '0'      // ADJUSTL(CTSEC) 

! Write acres
IF (DUMP_TRANSIENT_ACREAGE) THEN
   FN = TRIM(OUTPUTS_DIRECTORY) // 'acres_' // SEVEN // '.csv'
   INQUIRE(FILE=TRIM(FN), OPENED=LOPEN)
   IF (.NOT. LOPEN) THEN
      OPEN(LUOUTPUT-3,FILE=TRIM(FN),FORM='FORMATTED',STATUS='REPLACE',IOSTAT=IOS)
      WRITE(LUOUTPUT-3,'(A)') 'time(s),acres'
   ENDIF
   WRITE(LUOUTPUT-3,200) T, ACRES
ENDIF
200 FORMAT (F9.1, ',', F9.1)

ULXMAP = ANALYSIS_XLLCORNER + 0.5 * ANALYSIS_CELLSIZE
ULYMAP = ANALYSIS_YLLCORNER + REAL(ANALYSIS_NROWS) * ANALYSIS_CELLSIZE - 0.5*ANALYSIS_CELLSIZE
I2DUMPME%ULXMAP=ULXMAP; I2DUMPME%ULYMAP=ULYMAP
RDUMPME%ULXMAP=ULXMAP; RDUMPME%ULYMAP=ULYMAP

IF (DUMP_SURFACE_FIRE) THEN
   I2DUMPME%I2(:,:,1) = SURFACE_FIRE(:,:)
   FN = 'surface_fire_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(I2DUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF
      
IF (DUMP_CROWN_FIRE .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   I2DUMPME%I2(:,:,1) = NINT(I2DUMPME%NODATA_VALUE,2)
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      I2DUMPME%I2(C%IX,C%IY,1) = C%CROWN_FIRE
      C => C%NEXT
   ENDDO
   FN = 'crown_fire_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(I2DUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_SPREAD_RATE .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,1) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%VELOCITY
      C => C%NEXT
   ENDDO
   FN = 'vs_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_HPUA  .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,1) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%HPUA_SURFACE + C%HPUA_CANOPY
      C => C%NEXT
   ENDDO
   FN = 'hpua_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_FLIN  .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,1) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%FLIN_SURFACE + C%FLIN_CANOPY
      C => C%NEXT
   ENDDO
   FN = 'flin_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_FLAME_LENGTH  .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,1) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%FLAME_LENGTH
      C => C%NEXT
   ENDDO
   FN = 'flame_length_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_REACTION_INTENSITY  .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%IR
      C => C%NEXT
   ENDDO
   FN = 'ir_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_WS20) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_TAGGED%HEAD
   DO I = 1, LIST_TAGGED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%WS20_NOW
      C => C%NEXT
   ENDDO
   FN = 'ws20_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_WD20) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_TAGGED%HEAD
   DO I = 1, LIST_TAGGED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%WD20_NOW
      C => C%NEXT
   ENDDO
   FN = 'wd20_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_VELOCITY .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%VELOCITY ! Convert to ft/min
      C => C%NEXT
   ENDDO
   FN = 'velocity_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_TIME_OF_ARRIVAL .AND. IDUMPCOUNT .EQ. NDUMPS) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%TIME_OF_ARRIVAL
      C => C%NEXT
   ENDDO
   FN = 'time_of_arrival_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

#ifdef _WUI
IF (USE_BLDG_SPREAD_MODEL .AND. DUMP_TOTAL_DFC_RECEIVED) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_TAGGED%HEAD
   DO I = 1, LIST_TAGGED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%TOTAL_DFC_RECEIVED
      C => C%NEXT
   ENDDO
   FN = 'total_dfc_received_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (USE_BLDG_SPREAD_MODEL .AND. DUMP_TOTAL_RAD_RECEIVED) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_TAGGED%HEAD
   DO I = 1, LIST_TAGGED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%TOTAL_RAD_RECEIVED
      C => C%NEXT
   ENDDO
   FN = 'total_rad_received_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (USE_BLDG_SPREAD_MODEL .AND. DUMP_HRR_TRANSIENT) THEN
   RDUMPME%R4(:,:,:) = RDUMPME%NODATA_VALUE
   C => LIST_BURNED%HEAD
   DO I = 1, LIST_BURNED%NUM_NODES
      RDUMPME%R4(C%IX,C%IY,1) = C%HRR_TRANSIENT
      C => C%NEXT
   ENDDO
   FN = 'hrr_transient_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF
#endif

IF (DUMP_TAGGED) THEN
   I2DUMPME%I2(:,:,:) = NINT(I2DUMPME%NODATA_VALUE,2)
   C => LIST_TAGGED%HEAD
   DO I = 1, LIST_TAGGED%NUM_NODES
      I2DUMPME%I2(C%IX,C%IY,1) = 1_2
      C => C%NEXT
   ENDDO
   FN = 'tagged_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(I2DUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_PHI) THEN
   RDUMPME%R4(:,:,1) = PHIP(:,:)
   FN = 'phi_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(RDUMPME,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF

IF (DUMP_EMBER_FLUX .AND. IDUMPCOUNT .EQ. NDUMPS .AND. (.NOT. ACCUMULATE_EMBER_FLUX) ) THEN
   FN = 'ember_flux_' // SEVEN // '_' // CTSEC
   CALL WRITE_BIL_RASTER(EMBER_FLUX,OUTPUTS_DIRECTORY,FN,CONVERT_TO_GEOTIFF,.TRUE.,IRANK_WORLD)
ENDIF
  
IF (IDUMPCOUNT .EQ. NDUMPS .AND. DUMP_TRANSIENT_ACREAGE) CLOSE(LUOUTPUT-3) !Close acreage file

IDUMPCOUNT = IDUMPCOUNT + 1

! *****************************************************************************
END SUBROUTINE MAIN_DUMP_ROUTINE
! *****************************************************************************

! *****************************************************************************
SUBROUTINE FIRE_SIZE_STATS_TO_RASTERS(PREFIX, VALS)
! *****************************************************************************

CHARACTER(60), INTENT(IN) :: PREFIX
CHARACTER(400) :: FN
REAL, DIMENSION(:), INTENT(IN) :: VALS

INTEGER :: I, ICASE, IWX_BAND, IX, IY
TYPE (RASTER_TYPE), SAVE :: DUMMY
CHARACTER(4) :: SUFFIX

IF (.NOT. ASSOCIATED(DUMMY%R4)) THEN
   CALL ALLOCATE_EMPTY_RASTER(DUMMY, ASP%NCOLS, ASP%NROWS, 1, ASP%XLLCORNER, ASP%YLLCORNER, ASP%CELLSIZE, 0., 'FLOAT     ')
ENDIF

IF (DUMP_HOURLY_RASTERS) THEN
   ICASE = 0
   DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
      DUMMY%R4(:,:,1) = 0.
      DO I = 1, NUM_CASES_PER_STARTING_WX_BAND(IWX_BAND)
         ICASE = ICASE + 1
         IX = ICOL_FROM_X(STATS_X(ICASE),ANALYSIS_XLLCORNER,ANALYSIS_CELLSIZE)
         IY = IROW_FROM_Y(STATS_Y(ICASE),ANALYSIS_YLLCORNER,ANALYSIS_CELLSIZE)
         DUMMY%R4(IX,IY,1) = VALS(ICASE)
      ENDDO
      WRITE(SUFFIX, '(I4.4)') IWX_BAND
      FN = TRIM(PREFIX) // '_'  // SUFFIX
      CALL WRITE_BIL_RASTER(DUMMY, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD) 
   ENDDO
ENDIF

DUMMY%R4(:,:,1) = 0.
DO ICASE = 1, NUM_CASES_TOTAL
   IX = ICOL_FROM_X(STATS_X(ICASE),ANALYSIS_XLLCORNER,ANALYSIS_CELLSIZE)
   IY = IROW_FROM_Y(STATS_Y(ICASE),ANALYSIS_YLLCORNER,ANALYSIS_CELLSIZE)
   DUMMY%R4(IX,IY,1) = STATS_SURFACE_FIRE_AREA(ICASE)
ENDDO
FN = TRIM(PREFIX)
CALL WRITE_BIL_RASTER(DUMMY, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD) 

! *****************************************************************************
END SUBROUTINE FIRE_SIZE_STATS_TO_RASTERS
! *****************************************************************************

! *****************************************************************************
SUBROUTINE FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, VALS, IWX_BAND_TO_DUMP)
! *****************************************************************************

INTEGER, INTENT(IN) :: IWX_BAND_TO_DUMP
CHARACTER(60), INTENT(IN) :: PREFIX
CHARACTER(400) :: FN
REAL, DIMENSION(:), INTENT(IN) :: VALS

INTEGER :: I, ICASE, IWX_BAND, IX, IY
TYPE (RASTER_TYPE), SAVE :: DUMMY
CHARACTER(4) :: SUFFIX

IF (.NOT. ASSOCIATED(DUMMY%R4)) THEN
   CALL ALLOCATE_EMPTY_RASTER(DUMMY, ASP%NCOLS, ASP%NROWS, 1, ASP%XLLCORNER, ASP%YLLCORNER, ASP%CELLSIZE, 0., 'FLOAT     ')
ENDIF
DUMMY%R4(:,:,1) = 0.

IF (IWX_BAND_TO_DUMP .EQ. 0) THEN
   DO ICASE = 1, NUM_CASES_TOTAL
      IX = ICOL_FROM_X(STATS_X(ICASE),ANALYSIS_XLLCORNER,ANALYSIS_CELLSIZE)
      IY = IROW_FROM_Y(STATS_Y(ICASE),ANALYSIS_YLLCORNER,ANALYSIS_CELLSIZE)
      DUMMY%R4(IX,IY,1) = STATS_SURFACE_FIRE_AREA(ICASE)
   ENDDO
   FN = TRIM(PREFIX)
ELSE
   ICASE = 0
   DO IWX_BAND = IWX_BAND_START, IWX_BAND_TO_DUMP, IWX_BAND_SKIP
      IF (IWX_BAND .NE. IWX_BAND_TO_DUMP) ICASE = ICASE + NUM_CASES_PER_STARTING_WX_BAND(IWX_BAND)
   ENDDO

   DO I = 1, NUM_CASES_PER_STARTING_WX_BAND(IWX_BAND_TO_DUMP)
      ICASE = ICASE + 1
      IX = ICOL_FROM_X(STATS_X(ICASE),ANALYSIS_XLLCORNER,ANALYSIS_CELLSIZE)
      IY = IROW_FROM_Y(STATS_Y(ICASE),ANALYSIS_YLLCORNER,ANALYSIS_CELLSIZE)
      DUMMY%R4(IX,IY,1) = VALS(ICASE)
   ENDDO

   WRITE(SUFFIX, '(I4.4)') IWX_BAND_TO_DUMP
   FN = TRIM(PREFIX) // '_'  // SUFFIX
ENDIF

CALL WRITE_BIL_RASTER(DUMMY, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD) 

! *****************************************************************************
END SUBROUTINE FIRE_SIZE_STATS_TO_RASTERS_HOURLY
! *****************************************************************************

! *****************************************************************************
SUBROUTINE POSTPROCESS
! *****************************************************************************

INTEGER :: I, IX, IY, IOS, IBAND, IWX_BAND, J, LUSTATS, IERR=0, IPYROME
LOGICAL :: LOPEN
CHARACTER(4) :: SUFFIX
CHARACTER(7) :: CID
CHARACTER(16) :: TIMESTAMP
CHARACTER(81) :: PREFIX
CHARACTER(88) :: FIRE_ID
CHARACTER(400) :: FN, HEADER
TYPE(RASTER_TYPE) :: INTERMEDIATE_I2, INTERMEDIATE_R4
TYPE(RASTER_TYPE), POINTER :: R

IF (IRANK_WORLD .EQ. 0 .AND. DUMP_FIRE_SIZE_STATS) THEN

   LUSTATS = LUOUTPUT - 2
   INQUIRE(UNIT=LUSTATS,OPENED=LOPEN)
   IF (LOPEN) CLOSE(LUSTATS)

   FN = TRIM(OUTPUTS_DIRECTORY) // 'fire_size_stats.csv'
   OPEN(LUSTATS,FILE=TRIM(FN),FORM='FORMATTED',STATUS='REPLACE',IOSTAT=IOS)
   WRITE(LUSTATS,'(A)') 'icase,Meteorology band,x,y,tstop (h),Wall clock time (s),Total fire area (ac),&
                         &Crown fire area (ac),Fire volume (ac-ft),Population affected,Real estate value,&
                         &Land value,Nembers,Pyrome,Containfrac,PM 2.5 release (ug),HRR peak (MW),&
                         &Start timestamp,Fire id,Astop (ac)'

   IPYROME=1

   IF (TRIM(RUN_ID) .EQ. '') THEN
      PREFIX = ''
   ELSE
      PREFIX = TRIM(RUN_ID) // '_'
   ENDIF

   DO I = 1, NUM_CASES_TOTAL
      IF (RANDOM_IGNITIONS) THEN
         IF (USE_PYROMES) THEN
            IX = ICOL_FROM_X(STATS_X(I),ANALYSIS_XLLCORNER,ANALYSIS_CELLSIZE)
            IY = IROW_FROM_Y(STATS_Y(I),ANALYSIS_YLLCORNER,ANALYSIS_CELLSIZE)
            IPYROME = PYROMES%I2(IX,IY,1)
         ENDIF
      ELSE
         STATS_X(I) = 0.
         STATS_Y(I) = 0.
      ENDIF
      TIMESTAMP = HOUR_OF_YEAR_TO_TIMESTAMP (CURRENT_YEAR, BAND_ONE_HOUR_OF_YEAR + STATS_IWX_BAND_START(I) - 1)
      WRITE(CID, '(I7.7)') I
      FIRE_ID = TRIM(PREFIX) // CID
      WRITE(LUSTATS,9999) STATS_ICASE(I), STATS_IWX_BAND_START(I), STATS_X(I), STATS_Y(I), STATS_SIMULATION_TSTOP_HOURS(I), &
                          STATS_WALL_CLOCK_TIME(I), STATS_SURFACE_FIRE_AREA(I), STATS_CROWN_FIRE_AREA(I), & 
                          STATS_FIRE_VOLUME(I), STATS_AFFECTED_POPULATION(I), STATS_AFFECTED_REAL_ESTATE_VALUE(I), &
                          STATS_AFFECTED_LAND_VALUE(I), INT(STATS_NEMBERS(I)),IPYROME, STATS_FINAL_CONTAINMENT_FRAC(I), &
                          STATS_PM2P5_RELEASE(I), STATS_HRR_PEAK(I), TRIM(TIMESTAMP), TRIM(FIRE_ID), STATS_ASTOP(I)
   ENDDO
   CLOSE(LUSTATS)
!         ICASE      BAND       X,Y          TSTOP      CLOCK      Fire           Emb   Pyrm   Containfrac     PM,HRR     TS      ID       Astop
9999 FORMAT(I7, ',', I5, ',', 2(F13.2, ','), F8.2, ',', F11.4, ',', 6(F13.1, ','), I7,',',I3, ',', F11.3, ',', 2(E11.3, ','), A, ',', A, ',', E10.3)

ENDIF

IF (IRANK_WORLD .EQ. 0 .AND. NUM_MONTE_CARLO_VARIABLES .GT. 0) THEN
   HEADER='ICASE'
   IF (NUM_PARAMETERS_RASTERS .GT. 0) THEN
      DO I = 1, NUM_PARAMETERS_RASTERS
         HEADER = TRIM(HEADER) // ',' // TRIM(RASTER_TO_PERTURB(I))
      ENDDO
   ENDIF

   IF (ENABLE_SPOTTING) HEADER = TRIM(HEADER) // ',MSD,NSDV,SWE,SFE,NEMX,GSFSP,CFSP,PIGN'
   IF (PERTURB_WIND_DIRECTION_FLUCTUATION_INTENSITY) HEADER = TRIM(HEADER) // ',WDFI'
   IF (PERTURB_WIND_SPEED_FLUCTUATION_INTENSITY)     HEADER = TRIM(HEADER) // ',WSFI'
   HEADER=TRIM(HEADER) // ','

   FN = TRIM(OUTPUTS_DIRECTORY) // 'coeffs.csv'
   OPEN(LUSTATS,FILE=TRIM(FN),FORM='FORMATTED',STATUS='REPLACE',IOSTAT=IOS)
   WRITE(LUSTATS,'(A)') TRIM(HEADER)
   DO I = 1, NUM_CASES_TOTAL
      WRITE(LUSTATS,9998) STATS_ICASE(I), (COEFFS_UNSCALED_BY_CASE(I,J), J = 1, NUM_MONTE_CARLO_VARIABLES) 
   ENDDO
   CLOSE(LUSTATS)

ENDIF
9998 FORMAT (I7,',',25(F9.4,','))

IF (CALCULATE_TIMES_BURNED .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(34)) THEN
   FN='times_burned'
   R=>TIMES_BURNED
   CALL ALLOCATE_EMPTY_RASTER(INTERMEDIATE_R4, R%NCOLS, R%NROWS, 1, R%XLLCORNER, R%YLLCORNER, R%CELLSIZE, 0., 'FLOAT     ')
   INTERMEDIATE_R4%R4(:,:,1) = TIMES_BURNED%R4(:,:,1)
   CALL WRITE_BIL_RASTER(INTERMEDIATE_R4, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD)
   
   IF (DUMP_HOURLY_RASTERS) THEN
      IBAND = 0
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         IBAND = IBAND + 1
         WRITE(SUFFIX, '(I4.4)') IWX_BAND
         FN = 'times_burned' // '_'  // SUFFIX
         INTERMEDIATE_R4%R4(:,:,1) = TIMES_BURNED_HOURLY%R4(:,:,IBAND)
         CALL WRITE_BIL_RASTER(INTERMEDIATE_R4, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD) 
      ENDDO
   ENDIF
ENDIF

IF (DUMP_SURFACE_FIRE_AREA .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(35)) THEN
   PREFIX = 'surface_fire_area'
   CALL FIRE_SIZE_STATS_TO_RASTERS(PREFIX, STATS_SURFACE_FIRE_AREA(:) )
   IF (DUMP_HOURLY_RASTERS) THEN
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         CALL FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, STATS_SURFACE_FIRE_AREA(:), IWX_BAND )
      ENDDO
   ENDIF
ENDIF

IF (DUMP_CROWN_FIRE_AREA .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(36)) THEN
   PREFIX = 'crown_fire_area'
   CALL FIRE_SIZE_STATS_TO_RASTERS(PREFIX, STATS_CROWN_FIRE_AREA(:) )
   IF (DUMP_HOURLY_RASTERS) THEN
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         CALL FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, STATS_CROWN_FIRE_AREA(:), IWX_BAND )
      ENDDO
   ENDIF
ENDIF

IF (DUMP_FIRE_VOLUME .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(37)) THEN
   PREFIX = 'fire_volume'
   CALL FIRE_SIZE_STATS_TO_RASTERS(PREFIX, STATS_FIRE_VOLUME(:) )
   IF (DUMP_HOURLY_RASTERS) THEN
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         CALL FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, STATS_FIRE_VOLUME(:), IWX_BAND )
      ENDDO
   ENDIF
ENDIF

IF (DUMP_AFFECTED_POPULATION .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(38)) THEN
   PREFIX = 'affected_population'
   CALL FIRE_SIZE_STATS_TO_RASTERS(PREFIX, STATS_AFFECTED_POPULATION(:) )
   IF (DUMP_HOURLY_RASTERS) THEN
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         CALL FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, STATS_AFFECTED_POPULATION(:), IWX_BAND )
      ENDDO
   ENDIF
ENDIF

IF (DUMP_AFFECTED_REAL_ESTATE_VALUE .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(39)) THEN
   PREFIX = 'affected_real_estate_value'
   CALL FIRE_SIZE_STATS_TO_RASTERS(PREFIX, STATS_AFFECTED_REAL_ESTATE_VALUE(:) )
   IF (DUMP_HOURLY_RASTERS) THEN
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         CALL FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, STATS_AFFECTED_REAL_ESTATE_VALUE(:), IWX_BAND )
      ENDDO
   ENDIF
ENDIF

IF (DUMP_AFFECTED_LAND_VALUE .AND. IRANK_WORLD .EQ. PARALLEL_IO_RANK(40)) THEN
   PREFIX = 'affected_land_value'
   CALL FIRE_SIZE_STATS_TO_RASTERS(PREFIX, STATS_AFFECTED_LAND_VALUE(:) )
   IF (DUMP_HOURLY_RASTERS) THEN
      DO IWX_BAND = IWX_BAND_START, IWX_BAND_STOP, IWX_BAND_SKIP
         CALL FIRE_SIZE_STATS_TO_RASTERS_HOURLY(PREFIX, STATS_AFFECTED_LAND_VALUE(:), IWX_BAND )
      ENDDO
   ENDIF
ENDIF

IF (IRANK_WORLD .EQ. 0) THEN
   IF (CALCULATE_TIMES_BURNED .AND. CALCULATE_FLAME_LENGTH_STATS) THEN
      FN='flame_length_max'
      CALL WRITE_BIL_RASTER(FLAME_LENGTH_MAX, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD)

      FN='flame_length_sum'
      CALL WRITE_BIL_RASTER(FLAME_LENGTH_SUM, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD)

      IF (USE_FLAME_LENGTH_BINS) THEN
          FN='flame_length_bin_counts'
          CALL WRITE_BIL_RASTER(FLAME_LENGTH_BIN_COUNT, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE.,IRANK_WORLD)
      ENDIF
   ENDIF
   IF (USE_EMBER_COUNT_BINS) THEN
       FN='ember_bin_counts'
       CALL WRITE_BIL_RASTER(EMBER_BIN_COUNT, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE.,IRANK_WORLD)
   ENDIF

ENDIF 

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)

IF (DUMP_EMBER_FLUX .AND. ACCUMULATE_EMBER_FLUX) THEN 
   R=>TIMES_BURNED
   CALL ALLOCATE_EMPTY_RASTER(INTERMEDIATE_I2, R%NCOLS, R%NROWS, 1, R%XLLCORNER, R%YLLCORNER, R%CELLSIZE, 0., 'SIGNEDINT ')

   CALL MPI_REDUCE(EMBER_FLUX%I2(:,:,1), INTERMEDIATE_I2%I2(:,:,1), ANALYSIS_NCOLS*ANALYSIS_NROWS, &
                   MPI_SHORT, MPI_SUM, 0, MPI_COMM_WORLD, IERR)
   IF (IRANK_WORLD .EQ. 0) THEN
      FN='ember_flux'
      CALL WRITE_BIL_RASTER(INTERMEDIATE_I2, OUTPUTS_DIRECTORY, FN, CONVERT_TO_GEOTIFF, .TRUE., IRANK_WORLD)
   ENDIF
ENDIF

! *****************************************************************************
END SUBROUTINE POSTPROCESS
! *****************************************************************************

! *****************************************************************************
SUBROUTINE WRITE_STATION(C,IRANK,ICASE,T)
! *****************************************************************************

INTEGER, INTENT(IN) :: IRANK, ICASE
REAL, INTENT(IN) :: T
TYPE(NODE), INTENT(IN) :: C
LOGICAL :: LOPEN
INTEGER :: IOS
CHARACTER(11) :: FN
CHARACTER(7) :: SEVEN

WRITE(SEVEN, '(I7.7)') ICASE

FN = SEVEN // '.csv'
INQUIRE(UNIT=LUNODES+IRANK,OPENED=LOPEN)

IF (.NOT. LOPEN) THEN 
   OPEN(LUNODES+IRANK,FILE=TRIM(FN),FORM='FORMATTED',STATUS='REPLACE',IOSTAT=IOS)
   WRITE(LUNODES+IRANK,'(A)') 'IRANK,ICASE,T,M1,M10,M100,MLH,MLW,WD20_NOW,WS20_NOW,WSMF'
ENDIF

WRITE(LUNODES+IRANK,'(I4, ",", I7, ",", F10.1, ",", 8(F8.3, ",") )') IRANK, ICASE, T, &
   C%M1, C%M10, C%M100, C%MLH, C%MLW, C%WD20_NOW, C%WS20_NOW, C%WSMF

! *****************************************************************************
END SUBROUTINE WRITE_STATION
! *****************************************************************************

! *****************************************************************************
SUBROUTINE PARSE_ENVI_HEADER(HDR_FILE_NAME, DESCRIPTION, SAMPLES, LINES, BANDS, HEADER_OFFSET, FILE_TYPE, DATA_TYPE, INTERLEAVE, BYTE_ORDER, BAND_NAMES, COORDINATE_SYSTEM, DATA_IGNORE_VALUE, DEFAULT_BANDS, PROJECTION_NAME, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE, EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE)
! *****************************************************************************
IMPLICIT NONE
CHARACTER(LEN=*), INTENT(IN) :: HDR_FILE_NAME 
INTEGER, INTENT(OUT) :: SAMPLES, LINES, BANDS, HEADER_OFFSET, DATA_TYPE, BYTE_ORDER 
REAL, INTENT(OUT) :: DATA_IGNORE_VALUE, EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE
CHARACTER(3), INTENT(OUT) :: INTERLEAVE
CHARACTER(1024), INTENT(OUT) :: COORDINATE_SYSTEM, DESCRIPTION
CHARACTER(1024), INTENT(OUT) :: DEFAULT_BANDS, BAND_NAMES
CHARACTER(64), INTENT(OUT) :: FILE_TYPE, PROJECTION_NAME

CHARACTER(LEN=1024) :: LINE, KEY, CURRENT_VALUE
INTEGER :: I, POS, IOS
LOGICAL :: IN_MULTILINE
CHARACTER(LEN=:), ALLOCATABLE :: MULTILINE_VALUE

CHARACTER(LEN=256) :: MAP_INFO
CHARACTER(64) :: DATUM, UNITS
CHARACTER(5) :: UTM_NORTH_SOUTH
INTEGER :: UTM_PROJECTION_ZONE
REAL :: TEMP(6)
INTEGER :: POS_START, POS_END

IN_MULTILINE = .FALSE.
MULTILINE_VALUE = ''

OPEN(UNIT=10, FILE=HDR_FILE_NAME, STATUS='OLD', ACTION='READ', IOSTAT=IOS)
IF (IOS /= 0) THEN
   PRINT *, 'ERROR OPENING HDR FILE: ', HDR_FILE_NAME
   STOP
ENDIF


DO
   READ(10, '(A)', IOSTAT=I) LINE
   IF (I /= 0) EXIT

   IF (IN_MULTILINE) THEN
      IF (INDEX(LINE, '}') > 0) THEN
         MULTILINE_VALUE = MULTILINE_VALUE // ' ' // TRIM(ADJUSTL(LINE(1:INDEX(LINE, '}')-1)))
         IN_MULTILINE = .FALSE.
         CURRENT_VALUE = TRIM(MULTILINE_VALUE)
      ELSE
         MULTILINE_VALUE = MULTILINE_VALUE // ' ' // TRIM(ADJUSTL(LINE))
      ENDIF
   ELSE
      POS = INDEX(LINE, '=')
      IF (POS > 0) THEN
         KEY = TRIM(ADJUSTL(LINE(1:POS-1)))
         CURRENT_VALUE = TRIM(ADJUSTL(LINE(POS+1:)))
         IF (INDEX(CURRENT_VALUE, '{') > 0 .AND. INDEX(CURRENT_VALUE, '}') == 0) THEN
            IN_MULTILINE = .TRUE.
            MULTILINE_VALUE = TRIM(ADJUSTL(CURRENT_VALUE(INDEX(CURRENT_VALUE, '{')+1:)))
         ELSE IF (INDEX(CURRENT_VALUE, '{') > 0) THEN
            CURRENT_VALUE = TRIM(CURRENT_VALUE(INDEX(CURRENT_VALUE, '{')+1:INDEX(CURRENT_VALUE, '}')-1))
         ENDIF
      ENDIF
   ENDIF

   CURRENT_VALUE = TRIM(ADJUSTL(CURRENT_VALUE))

   SELECT CASE (TRIM(ADJUSTL(KEY)))
      CASE ('description')
         DESCRIPTION = CURRENT_VALUE
      CASE ('samples')
         READ(CURRENT_VALUE, *) SAMPLES
      CASE ('lines')
         READ(CURRENT_VALUE, *) LINES
      CASE ('bands')
         READ(CURRENT_VALUE, *) BANDS
      CASE ('header offset')
         READ(CURRENT_VALUE, *) HEADER_OFFSET
      CASE ('file type')
         FILE_TYPE = CURRENT_VALUE
      CASE ('data type')
         READ(CURRENT_VALUE, *) DATA_TYPE
      CASE ('interleave')
         INTERLEAVE = CURRENT_VALUE
      CASE ('byte order')
         READ(CURRENT_VALUE, *) BYTE_ORDER
      CASE ('map info')
         MAP_INFO = CURRENT_VALUE
      CASE ('coordinate system', 'coordinate system string')
         COORDINATE_SYSTEM = CURRENT_VALUE
      CASE ('band names')
         BAND_NAMES = CURRENT_VALUE
      CASE ('data ignore value')
         READ(CURRENT_VALUE, *) DATA_IGNORE_VALUE
      CASE ('default bands')
         DEFAULT_BANDS = CURRENT_VALUE
   END SELECT

END DO

CLOSE(10)

!PARSE MAP_INFO
IF (TRIM(MAP_INFO) /= '') THEN
   CALL PARSE_MAP_INFO(MAP_INFO, PROJECTION_NAME, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE, EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE, UTM_PROJECTION_ZONE, UTM_NORTH_SOUTH, DATUM, UNITS)

   POS_START = 1
   POS_END = INDEX(MAP_INFO(POS_START:), ",")
   PROJECTION_NAME = TRIM(ADJUSTL(MAP_INFO(POS_START:POS_START + POS_END - 2)))
   POS_START = POS_START + POS_END

   DO I = 1, 6
      IF (I < 6) THEN
         POS_END = INDEX(MAP_INFO(POS_START:), ",")
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) TEMP(I)
         POS_START = POS_START + POS_END
      ELSE
         !EDGCASE: STRING HAS NO TRAILING COMMA
         READ(MAP_INFO(POS_START:), *, IOSTAT=IOS) TEMP(I)
      END IF
   END DO

   X_PIXEL_REFERENCE = TEMP(1)
   Y_PIXEL_REFERENCE = TEMP(2)
   EASTING = TEMP(3)
   NORTHING = TEMP(4)
   X_PIXEL_SIZE = TEMP(5)
   Y_PIXEL_SIZE = TEMP(6)

END IF

! *****************************************************************************
END SUBROUTINE PARSE_ENVI_HEADER
! *****************************************************************************


! *****************************************************************************
SUBROUTINE PARSE_MAP_INFO(MAP_INFO, PROJECTION_NAME, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE, EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE, UTM_PROJECTION_ZONE, UTM_NORTH_SOUTH, DATUM, UNITS)
! *****************************************************************************

IMPLICIT NONE
CHARACTER(LEN=*), INTENT(IN) :: MAP_INFO 
INTEGER, INTENT(OUT) :: UTM_PROJECTION_ZONE 
REAL, INTENT(OUT) :: EASTING, NORTHING, X_PIXEL_SIZE, Y_PIXEL_SIZE, X_PIXEL_REFERENCE, Y_PIXEL_REFERENCE
CHARACTER(5), INTENT(OUT) :: UTM_NORTH_SOUTH
CHARACTER(64), INTENT(OUT) :: PROJECTION_NAME, DATUM, UNITS

INTEGER :: COMMA_POS, NUM_VALUES, POS_START, POS_END, I, IOS

! SET SOME DEFAULTS
UTM_NORTH_SOUTH = "EMPTY"

! PARSE OUT THE PROJECTION NAME FIRST
COMMA_POS = INDEX(MAP_INFO, ',')

! DETERMINE THE NUM OF VALUES THAT ARE PRESENT
POS_START = 1
DO
   POS_END = INDEX(MAP_INFO(POS_START:), ',')
   IF (POS_END == 0) THEN
      NUM_VALUES = NUM_VALUES + 1
      EXIT
   ELSE
      NUM_VALUES = NUM_VALUES + 1
      POS_START = POS_START + POS_END
   ENDIF
END DO

POS_START = 1
POS_END = INDEX(MAP_INFO(POS_START:), ",")
PROJECTION_NAME = TRIM(ADJUSTL(MAP_INFO(POS_START:POS_START + POS_END - 2)))
POS_START = POS_START + POS_END

DO I = 1, NUM_VALUES - 1
   POS_END = INDEX(MAP_INFO(POS_START:), ",")
   SELECT CASE (I)
      CASE (1)
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) X_PIXEL_REFERENCE
      CASE (2)
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) Y_PIXEL_REFERENCE
      CASE (3)
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) EASTING
      CASE (4)
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) NORTHING
      CASE (5)
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) X_PIXEL_SIZE
      CASE (6)
         READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) Y_PIXEL_SIZE
      CASE (7)
         IF (PROJECTION_NAME == "UTM") THEN
            READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) UTM_PROJECTION_ZONE
         ELSE
            READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) DATUM
         END IF
      CASE (8)
         IF (PROJECTION_NAME == "UTM") THEN
            READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) UTM_NORTH_SOUTH
         ELSE
            READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) UNITS
         END IF
      CASE (9)
         IF (PROJECTION_NAME == "UTM") THEN
            READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) DATUM
         END IF
      CASE (10)
         IF (PROJECTION_NAME == "UTM") THEN
            READ(MAP_INFO(POS_START:POS_START + POS_END - 2), *, IOSTAT=IOS) UNITS
         END IF
   END SELECT
   POS_START = POS_START + POS_END
END DO

! *****************************************************************************
END SUBROUTINE PARSE_MAP_INFO
! *****************************************************************************

! *****************************************************************************
END MODULE ELMFIRE_IO
! *****************************************************************************
