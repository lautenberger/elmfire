! *****************************************************************************
PROGRAM ELMFIRE_POST
! *****************************************************************************

USE ELMFIRE_VARS
USE ELMFIRE_IO
USE ELMFIRE_SUBS
USE SORT
USE MPI_F08
USE, INTRINSIC :: ISO_C_BINDING, ONLY : C_PTR, C_F_POINTER

IMPLICIT NONE

INTEGER :: I, J, ICASE, IPERCENTILES, IT, NX, NY, NCASES, NUM_TIMESTEPS, PERCENTILE_SPREAD, IOS, IWX_BAND, &
           N_PERCENTILES, NUM_CASES_TO_PROCESS, ICASE_TO_PROCESS(10000), IX, IY, IERR, &
           ARRAYSHAPE_ONED_OUTPUTS(1), ARRAYSHAPE_TWOD_OUTPUTS(1:2), ARRAYSHAPE_THREED_TYPE1(1:3), &
           ARRAYSHAPE_THREED_TYPE2(1:3), BINARY_FILE_TYPE, NUM_BURNED, POSTPROCESS_TYPE, START_TIME_MINUTES_PAST_HOUR

INTEGER, POINTER, DIMENSION(:) :: STATS_ICASE, STATS_IWX_BAND, STATS_ICASE_SORTED, &
                                  BINARY_OUTPUTS_IX_GRIDFIRE=>NULL(), BINARY_OUTPUTS_IY_GRIDFIRE=>NULL(), &
                                  BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE=>NULL()
INTEGER*2, POINTER, DIMENSION(:,:) :: TIMES_BURNED_COUNTER
INTEGER*2, POINTER, DIMENSION(:,:,:) :: CROWN_FIRE_COUNTER
INTEGER*2, ALLOCATABLE, DIMENSION(:,:) :: PERCENTILE_BAND
INTEGER*1, POINTER, DIMENSION(:,:) :: CROWN_FIRE, BURNED_PIXELS

REAL :: XLLCORNER, YLLCORNER, CELLSIZE, DT, PERCENTILES(1:100), FLAME_LENGTH_MIN, SPREAD_RATE_MIN, FIREMODEL_SIMULATION_TSTART
REAL, POINTER, DIMENSION(:) :: T, STATS_ICASE_REAL, STATS_FIRE_AREA_SORTED, STATS_X_IGN, STATS_Y_IGN, &
                               STATS_FIRE_AREA
REAL, POINTER, DIMENSION(:,:) :: TOA, FLAME_LENGTH, VELOCITY_FPM

LOGICAL :: DUMP_BURNED_PIXELS, DUMP_HOURS_SINCE_BURNED, READ_ALREADY_BURNED, READ_PHI, HOURLY_PYREGENCE_OUTPUTS, &
           DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM(1:1000)

CHARACTER(2) :: TWO
CHARACTER(4) :: FOUR
CHARACTER(7) :: ID,SEVEN
CHARACTER(400) :: FIRE_SIZE_STATS_FILENAME, TIMES_BURNED_FILENAME, DUMMY, FN, ALREADY_BURNED_FILENAME !, PHI_FILENAME
CHARACTER(3), ALLOCATABLE, DIMENSION(:) :: THREE

TYPE(RASTER_TYPE), TARGET :: HOURS_SINCE_BURNED, BURNED, ALREADY_BURNED, PHI
TYPE(RASTER_TYPE) :: I2TEMP, RTEMP

TYPE ANNUAL_BURNPROB_POST_TYPE
   REAL, POINTER, DIMENSION(:) :: FLAME_LENGTH, VELOCITY_FPM
END TYPE

TYPE (ANNUAL_BURNPROB_POST_TYPE), ALLOCATABLE, DIMENSION(:,:) :: ANNUAL_BURNPROB_POST

INTEGER(KIND=MPI_ADDRESS_KIND) :: ONED_ARRAY_SIZE_4, ONED_ARRAY_SIZE_2, ONED_ARRAY_SIZE_1, THREED_ARRAY_SIZE

TYPE (C_PTR  ) :: BINARY_OUTPUTS_IX_PTR, BINARY_OUTPUTS_IY_PTR, BINARY_OUTPUTS_TOA_PTR, BINARY_OUTPUTS_FLAME_LENGTH_PTR, &
                  BINARY_OUTPUTS_VELOCITY_FPM_PTR, BINARY_OUTPUTS_CROWN_FIRE_PTR, TOA_PTR, FLAME_LENGTH_PTR, &
                  VELOCITY_FPM_PTR, CROWN_FIRE_PTR, BURNED_PTR, HOURS_SINCE_BURNED_PTR, TIMES_BURNED_PTR, &
                  BINARY_OUTPUTS_IX_GRIDFIRE_PTR, BINARY_OUTPUTS_IY_GRIDFIRE_PTR, BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE_PTR, &
                  ALREADY_BURNED_PTR, PHI_PTR, BURNED_PIXELS_PTR

TYPE (MPI_WIN) :: WIN_BINARY_OUTPUTS_VELOCITY_FPM, WIN_BINARY_OUTPUTS_IX, WIN_BINARY_OUTPUTS_IY, WIN_BINARY_OUTPUTS_TOA, &
                  WIN_BINARY_OUTPUTS_FLAME_LENGTH, WIN_BINARY_OUTPUTS_CROWN_FIRE, WIN_TOA, WIN_FLAME_LENGTH, &
                  WIN_VELOCITY_FPM, WIN_CROWN_FIRE, WIN_BURNED, WIN_HOURS_SINCE_BURNED, WIN_TIMES_BURNED, &
                  WIN_BINARY_OUTPUTS_IX_GRIDFIRE, WIN_BINARY_OUTPUTS_IY_GRIDFIRE, WIN_BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE, &
                  WIN_ALREADY_BURNED, WIN_PHI, WIN_BURNED_PIXELS

NAMELIST /ELMFIRE_POST_INPUTS/ NX, NY, NCASES, XLLCORNER, YLLCORNER, CELLSIZE, OUTPUTS_DIRECTORY, DT, NUM_TIMESTEPS, &
                               NUM_CASES_TO_PROCESS, ICASE_TO_PROCESS, POSTPROCESS_TYPE, PATH_TO_GDAL, SCRATCH, &
                               BINARY_FILE_TYPE, FIRE_SIZE_STATS_FILENAME, N_PERCENTILES, PERCENTILES, &
                               DUMP_HOURS_SINCE_BURNED, DUMP_FLAME_LENGTH, DUMP_SPREAD_RATE, DUMP_CROWN_FIRE, &
                               DUMP_TIME_OF_ARRIVAL, FULL_BINARY_OUTPUTS, READ_ALREADY_BURNED, ALREADY_BURNED_FILENAME, &
                               READ_PHI, PHI_FILENAME, &
                               PATH_TO_GDAL, HOURLY_PYREGENCE_OUTPUTS, START_TIME_MINUTES_PAST_HOUR, FLAME_LENGTH_MIN, &
                               SPREAD_RATE_MIN, DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM, FIREMODEL_SIMULATION_TSTART, &
                               DUMP_BURNED_PIXELS

! Initialize MPI:
CALL MPI_INIT(IERR)
CALL MPI_COMM_RANK(MPI_COMM_WORLD,IRANK_WORLD,IERR)
CALL MPI_COMM_SIZE(MPI_COMM_WORLD,NPROC,IERR)
CALL MPI_GET_PROCESSOR_NAME (PROCNAME,LENPROCNAME,IERR)

!Get input file name:
CALL GET_COMMAND_ARGUMENT(1,NAMELIST_FN)

IF (NAMELIST_FN(1:1)==' ') THEN
   WRITE(*,*) "Error, no input file specified."
   WRITE(*,*) "Hit Enter to continue."
   READ(5,*)
   STOP
ENDIF

NUM_CASES_TO_PROCESS         = -1
N_PERCENTILES                = 9
PERCENTILES(1)               = 10.0
PERCENTILES(2)               = 20.0
PERCENTILES(3)               = 30.0
PERCENTILES(4)               = 40.0
PERCENTILES(5)               = 50.0
PERCENTILES(6)               = 60.0
PERCENTILES(7)               = 70.0
PERCENTILES(8)               = 80.0
PERCENTILES(9)               = 90.0
DUMP_BURNED_PIXELS           = .FALSE.
DUMP_CROWN_FIRE              = .TRUE.
DUMP_HOURS_SINCE_BURNED      = .TRUE.
DUMP_FLAME_LENGTH            = .TRUE.
DUMP_SPREAD_RATE             = .TRUE.
DUMP_TIME_OF_ARRIVAL         = .FALSE.
POSTPROCESS_TYPE             = 1 !1 = Ensemble fire forecast, 2 = annual burn probability
BINARY_FILE_TYPE             = 1 !1 = ELMFIRE, 2=GRIDFIRE
READ_ALREADY_BURNED          = .FALSE.
READ_PHI                     = .FALSE.
HOURLY_PYREGENCE_OUTPUTS     = .FALSE.
START_TIME_MINUTES_PAST_HOUR = 0
FLAME_LENGTH_MIN             = 0.
SPREAD_RATE_MIN              = 0.
DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM(:) = .TRUE.
FIREMODEL_SIMULATION_TSTART  = 0.
FULL_BINARY_OUTPUTS          = .TRUE.
DO I = 1, 10000
   ICASE_TO_PROCESS(I)=I
ENDDO

! Open input file and read in namelist groups
OPEN(LUINPUT,FILE=TRIM(NAMELIST_FN),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS)
IF (IOS .GT. 0) THEN
   WRITE(*,*) 'Problem opening input file ', TRIM(NAMELIST_FN)
   STOP
ENDIF

READ(LUINPUT,NML=ELMFIRE_POST_INPUTS,IOSTAT=IOS)
CLOSE(LUINPUT)
IF (IOS > 0) THEN
    WRITE(*,*) 'Error: Problem with namelist group &ELMFIRE_POST_INPUTS.'
    STOP
ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)

IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Setting up shared memory and allocating arrays'

! Start by windowing arrays:
IF (IRANK_WORLD .EQ. 0) THEN
   ONED_ARRAY_SIZE_4=INT(NX*NY,MPI_ADDRESS_KIND)*4_MPI_ADDRESS_KIND
   ONED_ARRAY_SIZE_2=INT(NX*NY,MPI_ADDRESS_KIND)*2_MPI_ADDRESS_KIND
   ONED_ARRAY_SIZE_1=INT(NX*NY,MPI_ADDRESS_KIND)*1_MPI_ADDRESS_KIND
   THREED_ARRAY_SIZE=NX*NY
   THREED_ARRAY_SIZE=THREED_ARRAY_SIZE*NUM_TIMESTEPS
   THREED_ARRAY_SIZE=THREED_ARRAY_SIZE*2_MPI_ADDRESS_KIND
ELSE
   ONED_ARRAY_SIZE_4=0_MPI_ADDRESS_KIND
   ONED_ARRAY_SIZE_2=0_MPI_ADDRESS_KIND
   ONED_ARRAY_SIZE_1=0_MPI_ADDRESS_KIND
   THREED_ARRAY_SIZE=0_MPI_ADDRESS_KIND
ENDIF

DISP_UNIT=1

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_2, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_IX_PTR          , WIN_BINARY_OUTPUTS_IX           )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_2, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_IY_PTR          , WIN_BINARY_OUTPUTS_IY           )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_TOA_PTR         , WIN_BINARY_OUTPUTS_TOA          )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_FLAME_LENGTH_PTR, WIN_BINARY_OUTPUTS_FLAME_LENGTH )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_VELOCITY_FPM_PTR, WIN_BINARY_OUTPUTS_VELOCITY_FPM )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_1, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_CROWN_FIRE_PTR  , WIN_BINARY_OUTPUTS_CROWN_FIRE   )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, TOA_PTR                        , WIN_TOA                         )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, FLAME_LENGTH_PTR               , WIN_FLAME_LENGTH                )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, VELOCITY_FPM_PTR               , WIN_VELOCITY_FPM                )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_1, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, CROWN_FIRE_PTR                 , WIN_CROWN_FIRE                  )
CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_1, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BURNED_PIXELS_PTR              , WIN_BURNED_PIXELS               )
IF (READ_ALREADY_BURNED) CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, ALREADY_BURNED_PTR, WIN_ALREADY_BURNED )
IF (READ_PHI) CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, PHI_PTR, WIN_PHI )

IF (POSTPROCESS_TYPE .EQ. 1) THEN
   CALL MPI_WIN_ALLOCATE_SHARED (THREED_ARRAY_SIZE, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BURNED_PTR            , WIN_BURNED                      )
   CALL MPI_WIN_ALLOCATE_SHARED (THREED_ARRAY_SIZE, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, HOURS_SINCE_BURNED_PTR, WIN_HOURS_SINCE_BURNED          )
ELSE
   CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, TIMES_BURNED_PTR, WIN_TIMES_BURNED )
ENDIF

IF (BINARY_FILE_TYPE .EQ. 2) THEN
   CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_IX_GRIDFIRE_PTR        , WIN_BINARY_OUTPUTS_IX_GRIDFIRE         )
   CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_IY_GRIDFIRE_PTR        , WIN_BINARY_OUTPUTS_IY_GRIDFIRE         )
   CALL MPI_WIN_ALLOCATE_SHARED (ONED_ARRAY_SIZE_4, DISP_UNIT, MPI_INFO_NULL, MPI_COMM_WORLD, BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE_PTR, WIN_BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE )
ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
IF (IRANK_WORLD .NE. 0) THEN
   CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_IX          , 0, ONED_ARRAY_SIZE_2, DISP_UNIT, BINARY_OUTPUTS_IX_PTR           )
   CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_IY          , 0, ONED_ARRAY_SIZE_2, DISP_UNIT, BINARY_OUTPUTS_IY_PTR           )
   CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_TOA         , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, BINARY_OUTPUTS_TOA_PTR          )
   CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_FLAME_LENGTH, 0, ONED_ARRAY_SIZE_4, DISP_UNIT, BINARY_OUTPUTS_FLAME_LENGTH_PTR )
   CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_VELOCITY_FPM, 0, ONED_ARRAY_SIZE_4, DISP_UNIT, BINARY_OUTPUTS_VELOCITY_FPM_PTR )
   CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_CROWN_FIRE  , 0, ONED_ARRAY_SIZE_1, DISP_UNIT, BINARY_OUTPUTS_CROWN_FIRE_PTR   )
   CALL MPI_WIN_SHARED_QUERY(WIN_TOA                        , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, TOA_PTR                         )
   CALL MPI_WIN_SHARED_QUERY(WIN_FLAME_LENGTH               , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, FLAME_LENGTH_PTR                )
   CALL MPI_WIN_SHARED_QUERY(WIN_VELOCITY_FPM               , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, VELOCITY_FPM_PTR                )
   CALL MPI_WIN_SHARED_QUERY(WIN_CROWN_FIRE                 , 0, ONED_ARRAY_SIZE_1, DISP_UNIT, CROWN_FIRE_PTR                  )
   CALL MPI_WIN_SHARED_QUERY(WIN_BURNED_PIXELS              , 0, ONED_ARRAY_SIZE_1, DISP_UNIT, BURNED_PIXELS_PTR               )
   IF (READ_ALREADY_BURNED) CALL MPI_WIN_SHARED_QUERY(WIN_ALREADY_BURNED, 0, ONED_ARRAY_SIZE_4, DISP_UNIT, ALREADY_BURNED_PTR )
   IF (READ_PHI) CALL MPI_WIN_SHARED_QUERY(WIN_PHI, 0, ONED_ARRAY_SIZE_4, DISP_UNIT, PHI_PTR )

   IF (POSTPROCESS_TYPE .EQ. 1) THEN
      CALL MPI_WIN_SHARED_QUERY(WIN_BURNED                     , 0, THREED_ARRAY_SIZE, DISP_UNIT, BURNED_PTR                      )
      CALL MPI_WIN_SHARED_QUERY(WIN_HOURS_SINCE_BURNED         , 0, THREED_ARRAY_SIZE, DISP_UNIT, HOURS_SINCE_BURNED_PTR          )
   ELSE
      CALL MPI_WIN_SHARED_QUERY(WIN_TIMES_BURNED , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, TIMES_BURNED_PTR )
   ENDIF

   IF (BINARY_FILE_TYPE .EQ. 2) THEN
      CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_IX_GRIDFIRE        , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, BINARY_OUTPUTS_IX_GRIDFIRE_PTR         )
      CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_IY_GRIDFIRE        , 0, ONED_ARRAY_SIZE_4, DISP_UNIT, BINARY_OUTPUTS_IY_GRIDFIRE_PTR         )
      CALL MPI_WIN_SHARED_QUERY(WIN_BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE, 0, ONED_ARRAY_SIZE_4, DISP_UNIT, BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE_PTR )
   ENDIF

ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
ARRAYSHAPE_ONED_OUTPUTS=(/ NX*NY /)
ARRAYSHAPE_TWOD_OUTPUTS=(/ NX, NY /)
ARRAYSHAPE_THREED_TYPE1=(/ NX, NY, NUM_TIMESTEPS /)
ARRAYSHAPE_THREED_TYPE2=(/ NX, NY, 1 /)

CALL C_F_POINTER(BINARY_OUTPUTS_IX_PTR           , BINARY_OUTPUTS_IX          , ARRAYSHAPE_ONED_OUTPUTS)
CALL C_F_POINTER(BINARY_OUTPUTS_IY_PTR           , BINARY_OUTPUTS_IY          , ARRAYSHAPE_ONED_OUTPUTS)
CALL C_F_POINTER(BINARY_OUTPUTS_TOA_PTR          , BINARY_OUTPUTS_TOA         , ARRAYSHAPE_ONED_OUTPUTS)
CALL C_F_POINTER(BINARY_OUTPUTS_FLAME_LENGTH_PTR , BINARY_OUTPUTS_FLAME_LENGTH, ARRAYSHAPE_ONED_OUTPUTS)
CALL C_F_POINTER(BINARY_OUTPUTS_VELOCITY_FPM_PTR , BINARY_OUTPUTS_VELOCITY_FPM, ARRAYSHAPE_ONED_OUTPUTS)
CALL C_F_POINTER(BINARY_OUTPUTS_CROWN_FIRE_PTR   , BINARY_OUTPUTS_CROWN_FIRE  , ARRAYSHAPE_ONED_OUTPUTS)
CALL C_F_POINTER(TOA_PTR                         , TOA                        , ARRAYSHAPE_TWOD_OUTPUTS)
CALL C_F_POINTER(FLAME_LENGTH_PTR                , FLAME_LENGTH               , ARRAYSHAPE_TWOD_OUTPUTS)
CALL C_F_POINTER(VELOCITY_FPM_PTR                , VELOCITY_FPM               , ARRAYSHAPE_TWOD_OUTPUTS)
CALL C_F_POINTER(CROWN_FIRE_PTR                  , CROWN_FIRE                 , ARRAYSHAPE_TWOD_OUTPUTS)
CALL C_F_POINTER(BURNED_PIXELS_PTR               , BURNED_PIXELS              , ARRAYSHAPE_TWOD_OUTPUTS)

IF (READ_ALREADY_BURNED) CALL C_F_POINTER(ALREADY_BURNED_PTR, ALREADY_BURNED%R4, ARRAYSHAPE_THREED_TYPE2)
IF (READ_PHI) CALL C_F_POINTER(PHI_PTR, PHI%R4, ARRAYSHAPE_THREED_TYPE2)

IF (POSTPROCESS_TYPE .EQ. 1) THEN
   CALL C_F_POINTER(BURNED_PTR                      , BURNED%I2                , ARRAYSHAPE_THREED_TYPE1 )
   CALL C_F_POINTER(HOURS_SINCE_BURNED_PTR          , HOURS_SINCE_BURNED%I2    , ARRAYSHAPE_THREED_TYPE1 )
ELSE
   CALL C_F_POINTER(TIMES_BURNED_PTR, TIMES_BURNED%I2, ARRAYSHAPE_THREED_TYPE2 )
ENDIF

IF (BINARY_FILE_TYPE .EQ. 2) THEN
   CALL C_F_POINTER(BINARY_OUTPUTS_IX_GRIDFIRE_PTR        , BINARY_OUTPUTS_IX_GRIDFIRE        , ARRAYSHAPE_ONED_OUTPUTS)
   CALL C_F_POINTER(BINARY_OUTPUTS_IY_GRIDFIRE_PTR        , BINARY_OUTPUTS_IY_GRIDFIRE        , ARRAYSHAPE_ONED_OUTPUTS)
   CALL C_F_POINTER(BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE_PTR, BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE, ARRAYSHAPE_ONED_OUTPUTS)
ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
IF (NPROC .GT. 1) THEN
   CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_IX          , IERR)
   CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_IY          , IERR)
   CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_TOA         , IERR)
   CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_FLAME_LENGTH, IERR)
   CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_VELOCITY_FPM, IERR)
   CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_CROWN_FIRE  , IERR)
   CALL MPI_WIN_FENCE(0, WIN_TOA                        , IERR)
   CALL MPI_WIN_FENCE(0, WIN_FLAME_LENGTH               , IERR)
   CALL MPI_WIN_FENCE(0, WIN_VELOCITY_FPM               , IERR)
   CALL MPI_WIN_FENCE(0, WIN_CROWN_FIRE                 , IERR)
   CALL MPI_WIN_FENCE(0, WIN_BURNED_PIXELS              , IERR)
   IF (READ_ALREADY_BURNED) CALL MPI_WIN_FENCE(0, WIN_ALREADY_BURNED, IERR)
   IF (READ_PHI) CALL MPI_WIN_FENCE(0, WIN_PHI, IERR)

   IF (POSTPROCESS_TYPE .EQ. 1) THEN
      CALL MPI_WIN_FENCE(0, WIN_BURNED                     , IERR)
      CALL MPI_WIN_FENCE(0, WIN_HOURS_SINCE_BURNED         , IERR)
   ELSE
      CALL MPI_WIN_FENCE (0, WIN_TIMES_BURNED, IERR)
   ENDIF

   IF (BINARY_FILE_TYPE .EQ. 2) THEN
      CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_IX_GRIDFIRE        , IERR)
      CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_IY_GRIDFIRE        , IERR)
      CALL MPI_WIN_FENCE(0, WIN_BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE, IERR)
   ENDIF

ENDIF

!ALLOCATE(PARALLEL_IO_RANK(1:10000))
PARALLEL_IO_RANK(:) = 0

IF (POSTPROCESS_TYPE .EQ. 1 .AND. NUM_TIMESTEPS .GT. 0) THEN
   ALLOCATE(T    (1:NUM_TIMESTEPS))
   ALLOCATE(THREE(1:NUM_TIMESTEPS))

   IF (HOURLY_PYREGENCE_OUTPUTS) THEN
      IF (START_TIME_MINUTES_PAST_HOUR .EQ. 0) THEN
         T(1) = 0
         WRITE(THREE(1), '(I3.3)') 1
         DO IT = 2, NUM_TIMESTEPS
            T(IT) = T(IT-1) + DT
            WRITE(THREE(IT), '(I3.3)') IT
         ENDDO
      ELSE
         T(1) = 0
         T(2) = 3600.0 - START_TIME_MINUTES_PAST_HOUR * 60

         WRITE(THREE(1), '(I3.3)') 1
         WRITE(THREE(2), '(I3.3)') 2

         DO IT = 3, NUM_TIMESTEPS
            T(IT) = T(IT-1) + DT
            WRITE(THREE(IT), '(I3.3)') IT
         ENDDO
      ENDIF
   ELSE
      T(1) = DT
      WRITE(THREE(1), '(I3.3)') 1
      DO IT = 2, NUM_TIMESTEPS
         T(IT) = T(IT-1) + DT
         WRITE(THREE(IT), '(I3.3)') IT
      ENDDO
   ENDIF

   IF (NPROC .GT. 1) THEN
      I = 0
      DO IT = 1, NUM_TIMESTEPS
         IF (I .GT. NPROC - 1) I = 0
         PARALLEL_IO_RANK(IT) = I
         I = I + 1
      ENDDO
   ENDIF

ENDIF

ALLOCATE (STATS_ICASE                      (1:NCASES) )
ALLOCATE (STATS_IWX_BAND                   (1:NCASES) )
ALLOCATE (STATS_X_IGN                      (1:NCASES) )
ALLOCATE (STATS_Y_IGN                      (1:NCASES) )
ALLOCATE (STATS_TSTOP                      (1:NCASES) )
ALLOCATE (STATS_WALL_CLOCK_TIME            (1:NCASES) )
ALLOCATE (STATS_FIRE_AREA                  (1:NCASES) )
ALLOCATE (STATS_CROWN_FIRE_AREA            (1:NCASES) )
ALLOCATE (STATS_FIRE_VOLUME                (1:NCASES) )
ALLOCATE (STATS_AFFECTED_POPULATION        (1:NCASES) )
ALLOCATE (STATS_AFFECTED_REAL_ESTATE_VALUE (1:NCASES) )
ALLOCATE (STATS_AFFECTED_LAND_VALUE        (1:NCASES) )
ALLOCATE (STATS_FINAL_CONTAINMENT_FRAC     (1:NCASES) )
ALLOCATE (STATS_ICASE_REAL                 (1:NCASES) )
ALLOCATE (STATS_FIRE_AREA_SORTED           (1:NCASES) )
ALLOCATE (STATS_ICASE_SORTED               (1:NCASES) )

IF (IRANK_WORLD .EQ. 0) THEN 
   IF (BINARY_FILE_TYPE .EQ. 1) THEN
      WRITE(*,*) 'Reading ELMFIRE fire size stats'
      FN = TRIM(OUTPUTS_DIRECTORY) // 'fire_size_stats.csv'
      WRITE(*,*) 'Reading ', TRIM(FN)
      OPEN(LUINPUT, FILE=TRIM(FN), FORM='FORMATTED', STATUS='OLD') 
      READ (LUINPUT,*)
      DO ICASE = 1, NCASES
         READ(LUINPUT,*) STATS_ICASE(ICASE), STATS_IWX_BAND(ICASE), STATS_X_IGN(ICASE), STATS_Y_IGN(ICASE), STATS_TSTOP(ICASE), &
                         STATS_WALL_CLOCK_TIME(ICASE), STATS_FIRE_AREA(ICASE), STATS_CROWN_FIRE_AREA(ICASE), STATS_FIRE_VOLUME(ICASE), &
                         STATS_AFFECTED_POPULATION(ICASE), STATS_AFFECTED_REAL_ESTATE_VALUE(ICASE), STATS_AFFECTED_LAND_VALUE(ICASE)
      ENDDO 
      CLOSE(LUINPUT)
   ELSE
      WRITE(*,*) 'Reading GRIDFIRE fire size stats'
      FN = TRIM(OUTPUTS_DIRECTORY) // TRIM(FIRE_SIZE_STATS_FILENAME)
      WRITE(*,*) 'Reading ', TRIM(FN)
      OPEN(LUINPUT, FILE=TRIM(FN), FORM='FORMATTED', STATUS='OLD') 
      READ (LUINPUT,*)
      DO ICASE = 1, NCASES
         READ(LUINPUT,*) STATS_ICASE(ICASE), DUMMY, DUMMY, DUMMY, DUMMY, DUMMY, DUMMY, DUMMY, DUMMY, DUMMY, STATS_FIRE_AREA(ICASE)
      ENDDO
      CLOSE(LUINPUT)
   ENDIF

   WRITE (*,*) 'Sorting fire size stats'
   STATS_ICASE_REAL(:) = REAL(STATS_ICASE(:))
   STATS_FIRE_AREA_SORTED(:) = STATS_FIRE_AREA(:)
   CALL DSORT(STATS_FIRE_AREA_SORTED(:), STATS_ICASE_REAL(:), NCASES, 2)
   STATS_ICASE_SORTED(:) = INT(STATS_ICASE_REAL(:))

ENDIF

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)

IF (NPROC .GT. 1) THEN
   CALL MPI_BCAST(STATS_ICASE                       , NCASES, MPI_INTEGER, 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_IWX_BAND                    , NCASES, MPI_INTEGER, 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_X_IGN                       , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_Y_IGN                       , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_TSTOP                       , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_WALL_CLOCK_TIME             , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_FIRE_AREA                   , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_CROWN_FIRE_AREA             , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_FIRE_VOLUME                 , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_AFFECTED_POPULATION         , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_AFFECTED_REAL_ESTATE_VALUE  , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_AFFECTED_LAND_VALUE         , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_ICASE_REAL                  , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_FIRE_AREA_SORTED            , NCASES, MPI_REAL   , 0, MPI_COMM_WORLD, IERR)
   CALL MPI_BCAST(STATS_ICASE_SORTED                , NCASES, MPI_INTEGER, 0, MPI_COMM_WORLD, IERR)
ENDIF

IF (IRANK_WORLD .EQ. 0 .AND. POSTPROCESS_TYPE .EQ. 2) THEN
   TIMES_BURNED_FILENAME='times_burned'
   CALL READ_BSQ_RASTER (TIMES_BURNED, OUTPUTS_DIRECTORY, TIMES_BURNED_FILENAME)
ENDIF

IF (IRANK_WORLD .EQ. 0 .AND. READ_ALREADY_BURNED) CALL READ_BSQ_RASTER (ALREADY_BURNED, OUTPUTS_DIRECTORY, ALREADY_BURNED_FILENAME)
IF (IRANK_WORLD .EQ. 0 .AND. READ_PHI) CALL READ_BSQ_RASTER (PHI, OUTPUTS_DIRECTORY, PHI_FILENAME)

CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)

IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Reading binary ouputs from fire spread model and post-processing'

IF (POSTPROCESS_TYPE .EQ. 1) THEN ! Ensemble fire forecast

   IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Post-processing binary outputs - type 1 (ensemble fire forecast)'

   IF (BINARY_FILE_TYPE .EQ. 1) THEN ! ELMFIRE
      IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Post-processing binary file type 1 (ELMFIRE)'

      IF (NUM_CASES_TO_PROCESS .LT. 0) THEN !Use percentiles
         DO IPERCENTILES = 1, N_PERCENTILES
            PERCENTILE_SPREAD = PERCENTILES(IPERCENTILES)
            WRITE(TWO, '(I2.2)') PERCENTILE_SPREAD
            ID = TRIM(TWO)
            I = NINT (0.01 * REAL(PERCENTILE_SPREAD) * NCASES)
            ICASE    = STATS_ICASE_SORTED(I)
            IWX_BAND = STATS_IWX_BAND(ICASE)
            WRITE(FOUR,  '(I4.4)') IWX_BAND
            WRITE(SEVEN, '(I7.7)') ICASE

            IF (IRANK_WORLD .EQ. 0) THEN
               WRITE(*,*) 'Percentile spread: ', PERCENTILE_SPREAD
               WRITE(*,*) 'Acres: ', STATS_FIRE_AREA(ICASE)
               WRITE(*,*) 'ICASE: ', ICASE
               CALL POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE(FOUR, SEVEN)
               IF (DUMP_HOURS_SINCE_BURNED) CALL CALC_HOURS_SINCE_BURNED(NX, NY, NUM_TIMESTEPS)
            ENDIF

            CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
            CALL DUMP_ELMFIRE (ID, NUM_TIMESTEPS, NX, NY, XLLCORNER,YLLCORNER,CELLSIZE,IPERCENTILES)
         ENDDO !PERCENTILE_SPREAD

      ELSE !NUM_CASES_TO_PROCESS .LT. 0
         DO J = 1, NUM_CASES_TO_PROCESS
            CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
            ICASE    = ICASE_TO_PROCESS(J)
            IWX_BAND = STATS_IWX_BAND(ICASE)
            WRITE(FOUR,  '(I4.4)') IWX_BAND
            WRITE(SEVEN, '(I7.7)') ICASE
            ID = TRIM(SEVEN)
            IF (IRANK_WORLD .EQ. 0) THEN
               WRITE(*,*) 'Processing : ', ICASE
               WRITE(*,*) 'Acres: ', STATS_FIRE_AREA(ICASE)
               CALL POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE(FOUR, SEVEN)
               IF (DUMP_HOURS_SINCE_BURNED) CALL CALC_HOURS_SINCE_BURNED(NX,NY,NUM_TIMESTEPS)
            ENDIF
            CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
            CALL DUMP_ELMFIRE (ID, NUM_TIMESTEPS, NX, NY,XLLCORNER,YLLCORNER,CELLSIZE,J)
         ENDDO
      ENDIF

   ELSE !GRIDFIRE

      IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Post-processing binary file type 2 (GRIDFIRE)'

      IF (NUM_CASES_TO_PROCESS .LT. 0) THEN !Use percentiles

         DO IPERCENTILES = 1, N_PERCENTILES
            PERCENTILE_SPREAD = PERCENTILES(IPERCENTILES)
            WRITE(TWO, '(I2.2)') PERCENTILE_SPREAD
            ID = TRIM(TWO)
            I = NINT (0.01 * REAL(PERCENTILE_SPREAD) * NCASES)
            ICASE    = STATS_ICASE_SORTED(I)
            IWX_BAND = 1
            WRITE(FOUR,  '(I4.4)') IWX_BAND
            WRITE(SEVEN, '(I7.7)') ICASE

            IF (IRANK_WORLD .EQ. 0) THEN
               WRITE(*,*) 'Percentile spread: ', PERCENTILE_SPREAD
               WRITE(*,*) 'Acres: ', STATS_FIRE_AREA(ICASE)
               CALL POPULATE_RASTER_FROM_GRIDFIRE_BINARY_FILE(FOUR, SEVEN)
               IF (DUMP_HOURS_SINCE_BURNED) CALL CALC_HOURS_SINCE_BURNED(NX, NY, NUM_TIMESTEPS)
            ENDIF

            CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
            CALL DUMP_GRIDFIRE (ID, NUM_TIMESTEPS, NX, NY, XLLCORNER,YLLCORNER,CELLSIZE)
         ENDDO !PERCENTILE_SPREAD

      ELSE
         DO J = 1, NUM_CASES_TO_PROCESS
            CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
            ICASE    = ICASE_TO_PROCESS(J)
            IWX_BAND = 1
            WRITE(FOUR,  '(I4.4)') IWX_BAND
            WRITE(SEVEN, '(I7.7)') ICASE
            ID = TRIM(SEVEN)
            IF (IRANK_WORLD .EQ. 0) THEN
               WRITE(*,*) 'Processing : ', ICASE
               WRITE(*,*) 'Acres: ', STATS_FIRE_AREA(ICASE)
               CALL POPULATE_RASTER_FROM_GRIDFIRE_BINARY_FILE(FOUR, SEVEN)
               IF (DUMP_HOURS_SINCE_BURNED) CALL CALC_HOURS_SINCE_BURNED(NX,NY,NUM_TIMESTEPS)
            ENDIF
            CALL MPI_BARRIER(MPI_COMM_WORLD, IERR)
            CALL DUMP_ELMFIRE (ID, NUM_TIMESTEPS, NX, NY,XLLCORNER,YLLCORNER,CELLSIZE,J)
         ENDDO
      ENDIF
   ENDIF

ELSE !POSTPROCESS_TYPE = 2 (Annual burn prob)

   IF (IRANK_WORLD .EQ. 0) WRITE(*,*) 'Post-processing binary outputs - type 2 (annual burn probability)'

   IF (BINARY_FILE_TYPE .EQ. 1) THEN ! ELMFIRE

      IF (IRANK_WORLD .EQ. 0) THEN

         WRITE(*,*) 'Post-processing binary file type 1 (ELMFIRE)'

         ALLOCATE(ANNUAL_BURNPROB_POST(1:NX,1:NY))
         ALLOCATE(TIMES_BURNED_COUNTER(1:NX,1:NY)); TIMES_BURNED_COUNTER = 0
         ALLOCATE(CROWN_FIRE_COUNTER(1:NX,1:NY,0:2)); CROWN_FIRE_COUNTER = 0         
         ALLOCATE(PERCENTILE_BAND(1:NX,1:NY))

         CALL ALLOCATE_EMPTY_RASTER(I2TEMP, NX, NY, 1, XLLCORNER, YLLCORNER, CELLSIZE, 0., 'SIGNEDINT ')
         CALL ALLOCATE_EMPTY_RASTER(RTEMP , NX, NY, 1, XLLCORNER, YLLCORNER, CELLSIZE, 0., 'FLOAT     ')

         WRITE(*,*) 'Allocating postprocess arrays'
         DO IY = 1, NY
         DO IX = 1, NX
            ALLOCATE(ANNUAL_BURNPROB_POST(IX,IY)%FLAME_LENGTH(1:TIMES_BURNED%I2(IX,IY,1)))
            ALLOCATE(ANNUAL_BURNPROB_POST(IX,IY)%VELOCITY_FPM(1:TIMES_BURNED%I2(IX,IY,1)))
         ENDDO
         ENDDO

         WRITE(*,*) 'Populating rasters from binary files'
         DO ICASE = 1, NCASES
            IF (MOD(ICASE,1000) .EQ. 0) WRITE(*,*) 'ICASE: ', ICASE
            CALL POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE_ANNUAL_BURNPROB(ICASE, STATS_IWX_BAND(ICASE))
         ENDDO

         WRITE(*,*) 'Sorting rasters'
         DO IY = 1, NY
         DO IX = 1, NX
            CALL DSORT(ANNUAL_BURNPROB_POST(IX,IY)%FLAME_LENGTH(:), ANNUAL_BURNPROB_POST(IX,IY)%FLAME_LENGTH(:), INT(TIMES_BURNED%I2(IX,IY,1)), 1)
            CALL DSORT(ANNUAL_BURNPROB_POST(IX,IY)%VELOCITY_FPM(:), ANNUAL_BURNPROB_POST(IX,IY)%VELOCITY_FPM(:), INT(TIMES_BURNED%I2(IX,IY,1)), 1)
         ENDDO
         ENDDO

         WRITE(*,*) 'Looping over percentiles'

         DO IPERCENTILES = 1, N_PERCENTILES
            PERCENTILE_SPREAD = PERCENTILES(IPERCENTILES)
            WRITE(*,*) 'Percentile spread: ', PERCENTILE_SPREAD

            WRITE(TWO, '(I2.2)') PERCENTILE_SPREAD

            DO IY = 1, NY
            DO IX = 1, NX
               PERCENTILE_BAND(IX,IY) = MAX( NINT (0.01 * REAL(PERCENTILE_SPREAD) * REAL(TIMES_BURNED%I2(IX,IY,1))),1)
            ENDDO
            ENDDO

! Dump flame length percentiles
            RTEMP%R4 = 0.
            DO IX = 1, NX
            DO IY = 1, NY
               IF (TIMES_BURNED%I2(IX,IY,1) .GT. 0) RTEMP%R4(IX,IY,1) = ANNUAL_BURNPROB_POST(IX,IY)%FLAME_LENGTH(PERCENTILE_BAND(IX,IY))
            ENDDO
            ENDDO
            FN = 'flame-length_' // TWO
            CALL WRITE_BIL_RASTER(RTEMP, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)

! And spread rate percentiles
            RTEMP%R4 = 0.
            DO IX = 1, NX
            DO IY = 1, NY
               IF (TIMES_BURNED%I2(IX,IY,1) .GT. 0) RTEMP%R4(IX,IY,1) = ANNUAL_BURNPROB_POST(IX,IY)%VELOCITY_FPM(PERCENTILE_BAND(IX,IY))
            ENDDO
            ENDDO
            FN = 'spread-rate_' // TWO
            CALL WRITE_BIL_RASTER(RTEMP, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)

         ENDDO !PERCENTILE_SPREAD

         WRITE(*,*) 'Writing crown fire type'
         DO J = 0, 2
            I2TEMP%I2 = 0.
            DO IX = 1, NX
            DO IY = 1, NY
               I2TEMP%I2(IX,IY,1) = CROWN_FIRE_COUNTER(IX,IY,J)
            ENDDO
            ENDDO
            IF (J .EQ. 0) FN = 'count_surface_fire'
            IF (J .EQ. 1) FN = 'count_passive_crown_fire'
            IF (J .EQ. 2) FN = 'count_active_crown_fire'
            CALL WRITE_BIL_RASTER(I2TEMP, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
         ENDDO

      ENDIF !IRANK_WORLD .EQ. 0

   ELSE !GRIDFIRE

   ENDIF

ENDIF

IF (NPROC .GT. 1) THEN
   CALL MPI_WIN_FREE (WIN_BINARY_OUTPUTS_IX           )
   CALL MPI_WIN_FREE (WIN_BINARY_OUTPUTS_IY           )
   CALL MPI_WIN_FREE (WIN_BINARY_OUTPUTS_TOA          )
   CALL MPI_WIN_FREE (WIN_BINARY_OUTPUTS_FLAME_LENGTH )
   CALL MPI_WIN_FREE (WIN_BINARY_OUTPUTS_VELOCITY_FPM )
   CALL MPI_WIN_FREE (WIN_BINARY_OUTPUTS_CROWN_FIRE   )
   CALL MPI_WIN_FREE (WIN_TOA                         )
   CALL MPI_WIN_FREE (WIN_FLAME_LENGTH                )
   CALL MPI_WIN_FREE (WIN_VELOCITY_FPM                )
   CALL MPI_WIN_FREE (WIN_CROWN_FIRE                  )
   CALL MPI_WIN_FREE (WIN_BURNED_PIXELS               )
   IF (POSTPROCESS_TYPE .EQ. 1) THEN
      CALL MPI_WIN_FREE (WIN_BURNED                   )
      CALL MPI_WIN_FREE (WIN_HOURS_SINCE_BURNED       )
   ELSE
      CALL MPI_WIN_FREE (WIN_TIMES_BURNED             )
   ENDIF
ENDIF

CALL MPI_FINALIZE(IERR)

CONTAINS

! *****************************************************************************
SUBROUTINE POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE(FOUR, SEVEN)
! *****************************************************************************

CHARACTER(4), INTENT(IN) :: FOUR
CHARACTER(7), INTENT(IN) :: SEVEN
INTEGER :: I

FN = TRIM(OUTPUTS_DIRECTORY) // 'toa_' // FOUR // '_' // SEVEN // '.bin'
OPEN  (LUINPUT, FILE=TRIM(FN), FORM='UNFORMATTED', ACCESS='SEQUENTIAL', STATUS='OLD') 
READ  (LUINPUT) NUM_BURNED
READ  (LUINPUT) (BINARY_OUTPUTS_IX           (I), I=1, NUM_BURNED)
READ  (LUINPUT) (BINARY_OUTPUTS_IY           (I), I=1, NUM_BURNED)
IF (FULL_BINARY_OUTPUTS) THEN
   READ  (LUINPUT) (BINARY_OUTPUTS_TOA          (I), I=1, NUM_BURNED)
   READ  (LUINPUT) (BINARY_OUTPUTS_FLAME_LENGTH (I), I=1, NUM_BURNED)
   READ  (LUINPUT) (BINARY_OUTPUTS_VELOCITY_FPM (I), I=1, NUM_BURNED)
   READ  (LUINPUT) (BINARY_OUTPUTS_CROWN_FIRE   (I), I=1, NUM_BURNED)
ENDIF
CLOSE (LUINPUT)

IF (DUMP_HOURS_SINCE_BURNED .OR. DUMP_TIME_OF_ARRIVAL) THEN
   TOA (:,:) = -9999.
   DO I = 1, NUM_BURNED
      IF (BINARY_OUTPUTS_IX(I) .LT. 1 .OR. BINARY_OUTPUTS_IX(I) .GT. NX) CYCLE
      IF (BINARY_OUTPUTS_IY(I) .LT. 1 .OR. BINARY_OUTPUTS_IY(I) .GT. NY) CYCLE
      TOA ( BINARY_OUTPUTS_IX(I), BINARY_OUTPUTS_IY(I) ) = BINARY_OUTPUTS_TOA (I) - FIREMODEL_SIMULATION_TSTART
   ENDDO
ENDIF

IF (DUMP_FLAME_LENGTH) THEN
   FLAME_LENGTH (:,:) = 0.
   DO I = 1, NUM_BURNED
      IF (BINARY_OUTPUTS_IX(I) .LT. 1 .OR. BINARY_OUTPUTS_IX(I) .GT. NX) CYCLE
      IF (BINARY_OUTPUTS_IY(I) .LT. 1 .OR. BINARY_OUTPUTS_IY(I) .GT. NY) CYCLE
      FLAME_LENGTH ( BINARY_OUTPUTS_IX(I), BINARY_OUTPUTS_IY(I) ) = BINARY_OUTPUTS_FLAME_LENGTH (I)
   ENDDO
ENDIF

IF (DUMP_SPREAD_RATE) THEN
   VELOCITY_FPM (:,:) = 0.
   DO I = 1, NUM_BURNED
      IF (BINARY_OUTPUTS_IX(I) .LT. 1 .OR. BINARY_OUTPUTS_IX(I) .GT. NX) CYCLE
      IF (BINARY_OUTPUTS_IY(I) .LT. 1 .OR. BINARY_OUTPUTS_IY(I) .GT. NY) CYCLE
      VELOCITY_FPM ( BINARY_OUTPUTS_IX(I), BINARY_OUTPUTS_IY(I) ) = BINARY_OUTPUTS_VELOCITY_FPM (I)
   ENDDO
ENDIF

IF (DUMP_CROWN_FIRE) THEN
   CROWN_FIRE (:,:) = 0
   DO I = 1, NUM_BURNED
      IF (BINARY_OUTPUTS_IX(I) .LT. 1 .OR. BINARY_OUTPUTS_IX(I) .GT. NX) CYCLE
      IF (BINARY_OUTPUTS_IY(I) .LT. 1 .OR. BINARY_OUTPUTS_IY(I) .GT. NY) CYCLE
      CROWN_FIRE ( BINARY_OUTPUTS_IX(I), BINARY_OUTPUTS_IY(I) ) = BINARY_OUTPUTS_CROWN_FIRE (I)
   ENDDO
ENDIF

IF (DUMP_BURNED_PIXELS) THEN
   BURNED_PIXELS (:,:) = 0
   DO I = 1, NUM_BURNED
      IF (BINARY_OUTPUTS_IX(I) .LT. 1 .OR. BINARY_OUTPUTS_IX(I) .GT. NX) CYCLE
      IF (BINARY_OUTPUTS_IY(I) .LT. 1 .OR. BINARY_OUTPUTS_IY(I) .GT. NY) CYCLE
      BURNED_PIXELS ( BINARY_OUTPUTS_IX(I), BINARY_OUTPUTS_IY(I) ) = 1
   ENDDO
ENDIF

! *****************************************************************************
END SUBROUTINE POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE
! *****************************************************************************

! *****************************************************************************
SUBROUTINE POPULATE_RASTER_FROM_GRIDFIRE_BINARY_FILE(FOUR, SEVEN)
! *****************************************************************************

CHARACTER(4), INTENT(IN) :: FOUR
CHARACTER(7), INTENT(IN) :: SEVEN
CHARACTER(400) :: FN
INTEGER :: I, ISTART, J

FN = TRIM(OUTPUTS_DIRECTORY) // 'toa_' // FOUR // '_' // SEVEN // '.bin'
OPEN(LUINPUT, FILE=TRIM(FN), FORM='UNFORMATTED', ACCESS='DIRECT', STATUS='OLD', RECL=4, CONVERT='big_endian') 
READ(LUINPUT,REC=1) NUM_BURNED

ISTART=2
J = 0
DO I = ISTART, ISTART + NUM_BURNED - 1
   J = J + 1
   READ (LUINPUT,REC=I) BINARY_OUTPUTS_IX_GRIDFIRE(J)
ENDDO

ISTART=I
J = 0
DO I = ISTART, ISTART + NUM_BURNED - 1
   J = J + 1
   READ (LUINPUT,REC=I) BINARY_OUTPUTS_IY_GRIDFIRE(J)
ENDDO

ISTART=I
J = 0
DO I = ISTART, ISTART + NUM_BURNED - 1
   J = J + 1
   READ (LUINPUT,REC=I) BINARY_OUTPUTS_TOA(J)
ENDDO

ISTART=I
J = 0
DO I = ISTART, ISTART + NUM_BURNED - 1
   J = J + 1
   READ (LUINPUT,REC=I) BINARY_OUTPUTS_FLAME_LENGTH(J)
ENDDO

ISTART=I
J = 0
DO I = ISTART, ISTART + NUM_BURNED - 1
   J = J + 1
   READ (LUINPUT,REC=I) BINARY_OUTPUTS_VELOCITY_FPM(J)
ENDDO

ISTART=I
J = 0
DO I = ISTART, ISTART + NUM_BURNED - 1
   J = J + 1
   READ (LUINPUT,REC=I) BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE(J)
ENDDO

CLOSE(LUINPUT)

IF (DUMP_HOURS_SINCE_BURNED .OR. DUMP_TIME_OF_ARRIVAL) THEN
   TOA(:,:) = -9999.
   DO I = 1, NUM_BURNED
      TOA ( BINARY_OUTPUTS_IX_GRIDFIRE(I), BINARY_OUTPUTS_IY_GRIDFIRE(I) ) = BINARY_OUTPUTS_TOA (I)  - FIREMODEL_SIMULATION_TSTART
   ENDDO
ENDIF

IF (DUMP_FLAME_LENGTH) THEN
   FLAME_LENGTH(:,:) = 0.
   DO I = 1, NUM_BURNED
      FLAME_LENGTH ( BINARY_OUTPUTS_IX_GRIDFIRE(I), BINARY_OUTPUTS_IY_GRIDFIRE(I) ) = BINARY_OUTPUTS_FLAME_LENGTH (I)
   ENDDO
ENDIF

IF (DUMP_SPREAD_RATE) THEN
   VELOCITY_FPM(:,:) = 0.
   DO I = 1, NUM_BURNED
      VELOCITY_FPM ( BINARY_OUTPUTS_IX_GRIDFIRE(I), BINARY_OUTPUTS_IY_GRIDFIRE(I) ) = BINARY_OUTPUTS_VELOCITY_FPM (I)
   ENDDO
ENDIF

IF (DUMP_CROWN_FIRE) THEN
   CROWN_FIRE(:,:) = 0
   DO I = 1, NUM_BURNED
      CROWN_FIRE ( BINARY_OUTPUTS_IX_GRIDFIRE(I), BINARY_OUTPUTS_IY_GRIDFIRE(I) ) = BINARY_OUTPUTS_CROWN_FIRE_GRIDFIRE (I)
   ENDDO
ENDIF

! *****************************************************************************
END SUBROUTINE POPULATE_RASTER_FROM_GRIDFIRE_BINARY_FILE
! *****************************************************************************

! *****************************************************************************
SUBROUTINE POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE_ANNUAL_BURNPROB(ICASE, IWX_BAND)
! *****************************************************************************

INTEGER, INTENT(IN) :: ICASE, IWX_BAND
INTEGER :: I, IX, IY, ITB, ICF
CHARACTER(400) :: FN

WRITE(FOUR, '(I4.4)') IWX_BAND
WRITE(SEVEN, '(I7.7)') ICASE

FN = TRIM(OUTPUTS_DIRECTORY) // 'toa_' // FOUR // '_' // SEVEN // '.bin'
OPEN  (LUINPUT, FILE=TRIM(FN), FORM='UNFORMATTED', ACCESS='SEQUENTIAL', STATUS='OLD') 
READ  (LUINPUT) NUM_BURNED
READ  (LUINPUT) (BINARY_OUTPUTS_IX           (I), I=1, NUM_BURNED)
READ  (LUINPUT) (BINARY_OUTPUTS_IY           (I), I=1, NUM_BURNED)
READ  (LUINPUT) (BINARY_OUTPUTS_TOA          (I), I=1, NUM_BURNED)
READ  (LUINPUT) (BINARY_OUTPUTS_FLAME_LENGTH (I), I=1, NUM_BURNED)
READ  (LUINPUT) (BINARY_OUTPUTS_VELOCITY_FPM (I), I=1, NUM_BURNED)
READ  (LUINPUT) (BINARY_OUTPUTS_CROWN_FIRE   (I), I=1, NUM_BURNED)
CLOSE (LUINPUT)

FLAME_LENGTH(:,:) = 0.
VELOCITY_FPM(:,:) = 0.

DO I = 1, NUM_BURNED
   IX = BINARY_OUTPUTS_IX(I)
   IY = BINARY_OUTPUTS_IY(I)
   ITB = TIMES_BURNED_COUNTER(IX,IY) + 1
   IF (ITB .LE. TIMES_BURNED%I2(IX,IY,1) ) THEN
      TIMES_BURNED_COUNTER(IX,IY) = ITB
      ANNUAL_BURNPROB_POST(IX,IY)%FLAME_LENGTH(ITB) = BINARY_OUTPUTS_FLAME_LENGTH (I)
      ANNUAL_BURNPROB_POST(IX,IY)%VELOCITY_FPM(ITB) = BINARY_OUTPUTS_VELOCITY_FPM (I)
   ENDIF
   ICF = BINARY_OUTPUTS_CROWN_FIRE (I)
   CROWN_FIRE_COUNTER(IX,IY,ICF) = CROWN_FIRE_COUNTER(IX,IY,ICF) + 1
ENDDO

! *****************************************************************************
END SUBROUTINE POPULATE_RASTER_FROM_ELMFIRE_BINARY_FILE_ANNUAL_BURNPROB
! *****************************************************************************

! *****************************************************************************
SUBROUTINE CALC_HOURS_SINCE_BURNED(NX,NY,NUM_TIMESTEPS)
! *****************************************************************************

INTEGER, INTENT(IN) :: NX, NY, NUM_TIMESTEPS
INTEGER :: IT, IX, IY, ITSTART

BURNED%I2 = 0
!$OMP PARALLEL DO COLLAPSE(3) NUM_THREADS(NPROC) DEFAULT(NONE) PRIVATE(IT,IX,IY) SHARED(TOA,NX,NY,BURNED,T,NUM_TIMESTEPS)
DO IY = 1, NY
DO IX = 1, NX
DO IT = 1, NUM_TIMESTEPS
   IF (TOA(IX,IY) .GT. 0. .AND. TOA(IX,IY) .LE. T(IT)) BURNED%I2(IX,IY,IT) = 1
ENDDO
ENDDO
ENDDO
!$OMP END PARALLEL DO

HOURS_SINCE_BURNED%I2(:,:,1)=0
ITSTART=2
DO IT = ITSTART, NUM_TIMESTEPS
!$OMP PARALLEL DO COLLAPSE(2) NUM_THREADS(NPROC) DEFAULT(NONE) PRIVATE(IX,IY) SHARED(IT,BURNED,HOURS_SINCE_BURNED,NUM_TIMESTEPS,NY,NX)
   DO IY = 1, NY
   DO IX = 1, NX
      HOURS_SINCE_BURNED%I2(IX,IY,IT) = HOURS_SINCE_BURNED%I2(IX,IY,IT-1) + BURNED%I2(IX,IY,IT)
   ENDDO
   ENDDO
!$OMP END PARALLEL DO

   IF (READ_ALREADY_BURNED) THEN
!$OMP PARALLEL DO COLLAPSE(2) NUM_THREADS(NPROC) DEFAULT(NONE) PRIVATE(IX,IY) SHARED(IT,ALREADY_BURNED,PHI,HOURS_SINCE_BURNED,NUM_TIMESTEPS,NY,NX)
      DO IY = 1, NY
      DO IX = 1, NX
         IF (ALREADY_BURNED%R4(IX,IY,1) .GT.  1E-3 .AND. PHI%R4(IX,IY,1) .GT. -9998.) HOURS_SINCE_BURNED%I2(IX,IY,IT) = 400
      ENDDO
      ENDDO
!$OMP END PARALLEL DO
   ENDIF

   IF (READ_PHI) THEN
!$OMP PARALLEL DO COLLAPSE(2) NUM_THREADS(NPROC) DEFAULT(NONE) PRIVATE(IX,IY) SHARED(IT,PHI,HOURS_SINCE_BURNED,NUM_TIMESTEPS,NY,NX)
      DO IY = 1, NY
      DO IX = 1, NX
         IF (PHI%R4(IX,IY,1) .LT. -1E-3 .AND. PHI%R4(IX,IY,1) .GT. -9998.) HOURS_SINCE_BURNED%I2(IX,IY,IT) = 401
      ENDDO
      ENDDO
!$OMP END PARALLEL DO
   ENDIF


ENDDO

IF (HOURLY_PYREGENCE_OUTPUTS) THEN
   IT=1

   IF (READ_ALREADY_BURNED) THEN
!$OMP PARALLEL DO COLLAPSE(2) NUM_THREADS(NPROC) DEFAULT(NONE) PRIVATE(IX,IY) SHARED(IT,ALREADY_BURNED,PHI,HOURS_SINCE_BURNED,NUM_TIMESTEPS,NY,NX)
      DO IY = 1, NY
      DO IX = 1, NX
         IF (ALREADY_BURNED%R4(IX,IY,1) .GT.  1E-3 .AND. PHI%R4(IX,IY,1) .GT. -9998.) HOURS_SINCE_BURNED%I2(IX,IY,IT) = 400
      ENDDO
      ENDDO
!$OMP END PARALLEL DO
   ENDIF

   IF (READ_PHI) THEN
!$OMP PARALLEL DO COLLAPSE(2) NUM_THREADS(NPROC) DEFAULT(NONE) PRIVATE(IX,IY) SHARED(IT,PHI,HOURS_SINCE_BURNED,NUM_TIMESTEPS,NY,NX)
      DO IY = 1, NY
      DO IX = 1, NX
         IF (PHI%R4(IX,IY,1) .LT. -1E-3 .AND. PHI%R4(IX,IY,1) .GT. -9998.) HOURS_SINCE_BURNED%I2(IX,IY,IT) = 401
      ENDDO
      ENDDO
!$OMP END PARALLEL DO
   ENDIF

ENDIF

! *****************************************************************************
END SUBROUTINE CALC_HOURS_SINCE_BURNED
! *****************************************************************************

! *****************************************************************************
SUBROUTINE DUMP_ELMFIRE(ID,NUM_TIMESTEPS,NX,NY,XLLCORNER,YLLCORNER,CELLSIZE,IPERC)
! *****************************************************************************

INTEGER, INTENT(IN) :: NUM_TIMESTEPS, NX, NY, IPERC
REAL, INTENT(IN) :: XLLCORNER,YLLCORNER,CELLSIZE
CHARACTER(7), INTENT(IN) :: ID
INTEGER :: IT, SINGLEBAND_I2_NODATA_VALUE
TYPE (RASTER_TYPE), SAVE :: SINGLEBAND_I2, SINGLEBAND_REAL

IF (.NOT. ASSOCIATED(SINGLEBAND_I2%I2)) THEN
   CALL ALLOCATE_EMPTY_RASTER(SINGLEBAND_I2  , NX, NY, 1, XLLCORNER, YLLCORNER, CELLSIZE, 0., 'SIGNEDINT ')
   CALL ALLOCATE_EMPTY_RASTER(SINGLEBAND_REAL, NX, NY, 1, XLLCORNER, YLLCORNER, CELLSIZE, 0., 'FLOAT     ')
ENDIF

IF (NUM_TIMESTEPS .EQ. 0) THEN
   IF (DUMP_BURNED_PIXELS) THEN
      SINGLEBAND_I2_NODATA_VALUE = NINT(SINGLEBAND_I2%NODATA_VALUE)
      SINGLEBAND_I2%NODATA_VALUE = 255.
      SINGLEBAND_I2%I2(:,:,1) = NINT(SINGLEBAND_I2%NODATA_VALUE)
      SINGLEBAND_I2%I2(:,:,1) = INT(BURNED_PIXELS(:,:))
      FN = 'burned-pixels_' // TRIM(ID)
      CALL WRITE_BIL_RASTER(SINGLEBAND_I2, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
      SINGLEBAND_I2%NODATA_VALUE = SINGLEBAND_I2_NODATA_VALUE
   ENDIF    
ENDIF

DO IT = 1, NUM_TIMESTEPS

   IF (IRANK_WORLD .NE. PARALLEL_IO_RANK(IT) ) CYCLE

   IF (DUMP_HOURS_SINCE_BURNED .AND. DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM(IPERC) ) THEN
      SINGLEBAND_I2%I2(:,:,1) = HOURS_SINCE_BURNED%I2(:,:,IT)
      FN = 'hours-since-burned_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_I2, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_TIME_OF_ARRIVAL .AND. IT .EQ. 1) THEN
      SINGLEBAND_REAL%R4(:,:,1) = SINGLEBAND_REAL%NODATA_VALUE
      WHERE (TOA(:,:) .GT. 0. ) SINGLEBAND_REAL%R4(:,:,1) = TOA(:,:) / 3600.0
      FN = 'time-of-arrival_' // TRIM(ID)
      CALL WRITE_BIL_RASTER(SINGLEBAND_REAL, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_FLAME_LENGTH  .AND. DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM(IPERC) ) THEN
      SINGLEBAND_REAL%R4(:,:,1) = SINGLEBAND_REAL%NODATA_VALUE
      WHERE (TOA(:,:) .GT. 0. .AND. TOA(:,:) .LE. T(IT) ) SINGLEBAND_REAL%R4(:,:,1) = MAX(FLAME_LENGTH(:,:),FLAME_LENGTH_MIN)
      FN = 'flame-length_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_REAL, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_SPREAD_RATE .AND. DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM(IPERC) ) THEN
      SINGLEBAND_REAL%R4(:,:,1) = SINGLEBAND_REAL%NODATA_VALUE
      WHERE (TOA(:,:) .GT. 0. .AND. TOA(:,:) .LE. T(IT) ) SINGLEBAND_REAL%R4(:,:,1) = MAX(VELOCITY_FPM(:,:),SPREAD_RATE_MIN)
      FN = 'spread-rate_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_REAL, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_CROWN_FIRE .AND. DUMP_HOURLY_OUTPUTS_FOR_PERC_NUM(IPERC) ) THEN
      SINGLEBAND_I2_NODATA_VALUE = NINT(SINGLEBAND_I2%NODATA_VALUE)
      SINGLEBAND_I2%NODATA_VALUE = 255.
      SINGLEBAND_I2%I2(:,:,1) = NINT(SINGLEBAND_I2%NODATA_VALUE)
      WHERE (TOA(:,:) .GT. 0. .AND. TOA(:,:) .LE. T(IT) ) SINGLEBAND_I2%I2(:,:,1) = INT(CROWN_FIRE(:,:))
      FN = 'crown-fire_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_I2, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
      SINGLEBAND_I2%NODATA_VALUE = SINGLEBAND_I2_NODATA_VALUE
   ENDIF

ENDDO

! *****************************************************************************
END SUBROUTINE DUMP_ELMFIRE
! *****************************************************************************

! *****************************************************************************
SUBROUTINE DUMP_GRIDFIRE(ID,NUM_TIMESTEPS,NX,NY,XLLCORNER,YLLCORNER,CELLSIZE)
! *****************************************************************************

INTEGER, INTENT(IN) :: NUM_TIMESTEPS, NX, NY
REAL, INTENT(IN) :: XLLCORNER,YLLCORNER,CELLSIZE
CHARACTER(7), INTENT(IN) :: ID
INTEGER :: IT, SINGLEBAND_I2_NODATA_VALUE
TYPE (RASTER_TYPE), SAVE :: SINGLEBAND_I2, SINGLEBAND_REAL

IF (.NOT. ASSOCIATED(SINGLEBAND_I2%I2)) THEN
   CALL ALLOCATE_EMPTY_RASTER(SINGLEBAND_I2  , NX, NY, 1, XLLCORNER, YLLCORNER, CELLSIZE, 0., 'SIGNEDINT ')
   CALL ALLOCATE_EMPTY_RASTER(SINGLEBAND_REAL, NX, NY, 1, XLLCORNER, YLLCORNER, CELLSIZE, 0., 'FLOAT     ')
ENDIF

DO IT = 1, NUM_TIMESTEPS

   IF (IRANK_WORLD .NE. PARALLEL_IO_RANK(IT) ) CYCLE

   IF (DUMP_HOURS_SINCE_BURNED) THEN
      SINGLEBAND_I2%I2(:,:,1) = HOURS_SINCE_BURNED%I2(:,:,IT)
      FN = 'hours-since-burned_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_I2, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_TIME_OF_ARRIVAL .AND. IT .EQ. 1) THEN
      SINGLEBAND_REAL%R4(:,:,1) = SINGLEBAND_REAL%NODATA_VALUE
      WHERE (TOA(:,:) .GT. 0. ) SINGLEBAND_REAL%R4(:,:,1) = TOA(:,:) / 3600.0
      FN = 'time-of-arrival_' // TRIM(ID)
      CALL WRITE_BIL_RASTER(SINGLEBAND_REAL, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_FLAME_LENGTH) THEN
      SINGLEBAND_REAL%R4(:,:,1) = SINGLEBAND_REAL%NODATA_VALUE
      WHERE (TOA(:,:) .GT. 0. .AND. TOA(:,:) .LE. T(IT) ) SINGLEBAND_REAL%R4(:,:,1) = MAX(FLAME_LENGTH(:,:),FLAME_LENGTH_MIN)
      FN = 'flame-length_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_REAL, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_SPREAD_RATE) THEN
      SINGLEBAND_REAL%R4(:,:,1) = SINGLEBAND_REAL%NODATA_VALUE
      WHERE (TOA(:,:) .GT. 0. .AND. TOA(:,:) .LE. T(IT) ) SINGLEBAND_REAL%R4(:,:,1) = MAX(VELOCITY_FPM(:,:),SPREAD_RATE_MIN)
      FN = 'spread-rate_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_REAL, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
   ENDIF

   IF (DUMP_CROWN_FIRE) THEN
      SINGLEBAND_I2_NODATA_VALUE = NINT(SINGLEBAND_I2%NODATA_VALUE)
      SINGLEBAND_I2%NODATA_VALUE = 255.
      SINGLEBAND_I2%I2(:,:,1) = NINT(SINGLEBAND_I2%NODATA_VALUE)
      WHERE (TOA(:,:) .GT. 0. .AND. TOA(:,:) .LE. T(IT) ) SINGLEBAND_I2%I2(:,:,1) = INT(CROWN_FIRE(:,:))
      FN = 'crown-fire_' // TRIM(ID) // '_' // THREE(IT)
      CALL WRITE_BIL_RASTER(SINGLEBAND_I2, OUTPUTS_DIRECTORY, FN, .FALSE., .FALSE., IRANK_WORLD)
      SINGLEBAND_I2%NODATA_VALUE = SINGLEBAND_I2_NODATA_VALUE
   ENDIF

ENDDO

! *****************************************************************************
END SUBROUTINE DUMP_GRIDFIRE
! *****************************************************************************

! *****************************************************************************
END PROGRAM ELMFIRE_POST
! *****************************************************************************